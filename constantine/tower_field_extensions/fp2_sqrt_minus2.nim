# Constantine
# Copyright (c) 2018-2019    Status Research & Development GmbH
# Copyright (c) 2020-Present Mamy AndrÃ©-Ratsimbazafy
# Licensed and distributed under either of
#   * MIT license (license terms in the root directory or at http://opensource.org/licenses/MIT).
#   * Apache v2 license (license terms in the root directory or at http://www.apache.org/licenses/LICENSE-2.0).
# at your option. This file may not be copied, modified, or distributed except according to those terms.

# ############################################################
#
#        Quadratic Extension field over base field ğ”½p
#                        ğ”½p2 = ğ”½p[âˆš-5]
#
# ############################################################

# This implements a quadratic extension field over
# the base field ğ”½p:
#   ğ”½p2 = ğ”½p[x]
# with element A of coordinates (a0, a1) represented
# by a0 + a1 x
#
# The irreducible polynomial chosen is
#   xÂ² - Âµ with Âµ = -2
# i.e. ğ”½p2 = ğ”½p[âˆš-2]
#
# Consequently, for this file Fp2 to be valid
# -2 MUST not be a square in ğ”½p
#
# References
# [1] Software Implementation of Pairings\
#     D. Hankerson, A. Menezes, and M. Scott, 2009\
#     http://cacr.uwaterloo.ca/~ajmeneze/publications/pairings_software.pdf


import
  ../arithmetic/finite_fields,
  ../config/curves,
  ./abelian_groups

type
  Fp2*[C: static Curve] = object
    ## Element of the extension field
    ## ğ”½p2 = ğ”½p[âˆš-2] of a prime p
    ##
    ## with coordinates (c0, c1) such as
    ## c0 + c1 âˆš-2
    ##
    ## This requires -2 to not be a square (mod p)
    c0*, c1*: Fp[C]

func square*(r: var Fp2, a: Fp2) =
  ## Return a^2 in ğ”½p2 in ``r``
  ## ``r`` is initialized/overwritten
  # (c0, c1)Â² => (c0 + c1âˆš-2)Â²
  #           => c0Â² + 2 c0 c1âˆš-2 + (c1âˆš-2)Â²
  #           => c0Â² - 2c1Â² + 2 c0 c1 âˆš-2
  #           => (c0Â²-2c1Â², 2 c0 c1)
  #
  # Costs (naive implementation)
  # - 2 Multiplications ğ”½p
  # - 1 Squaring ğ”½p
  # - 1 Doubling ğ”½p
  # - 1 Substraction ğ”½p
  # Stack: 6 * ModulusBitSize (4x ğ”½p element + 2 named temporaries + 1 "in-place" mul temporary)

  var c1d, c0s {.noInit.}: typeof(a.c1)
  c1d.double(a.c1)       # c1d = 2 c1      [1 Dbl]
  c0s.square(a.c0)       # c0s = c0Â²       [1 Sqr, 1 Dbl]

  r.c1.prod(c1d, a.c0)   # r.c1 = 2 c1 c0  [1 Mul, 1 Sqr, 1 Dbl]
  c1d *= a.c1            # c1d = 2 c1Â²     [2 Mul, 1 Sqr, 1 Dbl] - 1 "in-place" temporary
  r.c0.diff(c0s, c1d)    # r.c0 = c0Â²-2c1Â² [2 Mul, 1 Sqr, 1 Dbl, 1 Sub]
