# Constantine
# Copyright (c) 2018-2019    Status Research & Development GmbH
# Copyright (c) 2020-Present Mamy Andr√©-Ratsimbazafy
# Licensed and distributed under either of
#   * MIT license (license terms in the root directory or at http://opensource.org/licenses/MIT).
#   * Apache v2 license (license terms in the root directory or at http://www.apache.org/licenses/LICENSE-2.0).
# at your option. This file may not be copied, modified, or distributed except according to those terms.

# Instantiate the actual tower extensions
# that were described in a "conceptualized" way
# ----------------------------------------------------------------

import
  ../arithmetic,
  ../config/curves,
  ../io/io_fields,
  ./tower_common,
  ./quadratic_extensions,
  ./cubic_extensions,
  ./exponentiations

export tower_common, quadratic_extensions, cubic_extensions, exponentiations

# ùîΩp2
# ----------------------------------------------------------------

type
  Fp2*[C: static Curve] = object
    c0*, c1*: Fp[C]

  Œ≤ = NonResidue
    # Quadratic or Cubic non-residue

  SexticNonResidue* = object

template fromComplexExtension*[F](elem: F): static bool =
  ## Returns true if the input is a complex extension
  ## i.e. the irreducible polynomial chosen is
  ##   x¬≤ - ¬µ with ¬µ = -1
  ## and so ùîΩp2 = ùîΩp[x] / x¬≤ - ¬µ = ùîΩp[ùëñ]
  when F is Fp2 and F.C.get_QNR_Fp() == -1:
    true
  else:
    false

func `*=`*(a: var Fp, _: typedesc[Œ≤]) {.inline.} =
  ## Multiply an element of ùîΩp by the quadratic non-residue
  ## chosen to construct ùîΩp2
  static: doAssert Fp.C.get_QNR_Fp() != -1, "ùîΩp2 should be specialized for complex extension"
  a *= Fp.C.get_QNR_Fp()

func `*`*(_: typedesc[Œ≤], a: Fp): Fp {.inline, noInit.} =
  ## Multiply an element of ùîΩp by the quadratic non-residue
  ## chosen to construct ùîΩp2
  result = a
  result *= Œ≤

# TODO: rework the quad/cube/sextic non residue declaration

func `*=`*(a: var Fp, _: typedesc[SexticNonResidue]) {.inline.} =
  ## Multiply an element of ùîΩp by the sextic non-residue
  ## chosen to construct ùîΩp6
  a *= Fp.C.get_QNR_Fp()

func `*`*(_: typedesc[SexticNonResidue], a: Fp): Fp {.inline, noInit.} =
  ## Multiply an element of ùîΩp by the sextic non-residue
  ## chosen to construct ùîΩp6
  result = a
  result *= SexticNonResidue

func `*=`*(a: var Fp2, _: typedesc[SexticNonResidue]) {.inline.} =
  ## Multiply an element of ùîΩp2 by the sextic non-residue
  ## chosen to construct the sextic twist
  # Yet another const tuple unpacking bug
  const u = Fp2.C.get_SNR_Fp2()[0] # Sextic non-residue to construct ùîΩp12
  const v = Fp2.C.get_SNR_Fp2()[1]
  const Beta {.used.} = Fp2.C.get_QNR_Fp()  # Quadratic non-residue to construct ùîΩp2
  # Œæ = u + v x
  # and x¬≤ = Œ≤
  #
  # (c0 + c1 x) (u + v x) => u c0 + (u c0 + u c1)x + v c1 x¬≤
  #                       => u c0 + Œ≤ v c1 + (v c0 + u c1) x
  when a.fromComplexExtension() and u == 1 and v == 1:
    let t = a.c0
    a.c0 -= a.c1
    a.c1 += t
  else:
    let a0 = a.c0
    let a1 = a.c1
    when a.fromComplexExtension():
      a.c0.diff(u * a0, v * a1)
    else:
      a.c0.sum(u * a0, (Beta * v) * a1)
    a.c1.sum(v * a0, u * a1)

func `/=`*(a: var Fp2, _: typedesc[SexticNonResidue]) {.inline.} =
  ## Multiply an element of ùîΩp by the quadratic non-residue
  ## chosen to construct sextic twist
  # Yet another const tuple unpacking bug
  const u = Fp2.C.get_SNR_Fp2()[0] # Sextic non-residue to construct ùîΩp12
  const v = Fp2.C.get_SNR_Fp2()[1]
  const Beta = Fp2.C.get_QNR_Fp()  # Quadratic non-residue to construct ùîΩp2
  # Œæ = u + v x
  # and x¬≤ = Œ≤
  #
  # (c0 + c1 x) / (u + v x) => (c0 + c1 x)(u - v x) / ((u + vx)(u-vx))
  #                         => u c0 - v c1 x¬≤ + (u c1 - v c0) x / (u¬≤ - x¬≤v¬≤)
  #                         => 1/(u¬≤ - Œ≤v¬≤) * (uc0 - Œ≤ v c1, u c1 - v c0)
  # With Œ≤ = ùëñ = ‚àö-1
  #   1/(u¬≤ + v¬≤) * (u c0 + v c1, u c1 - v c0)
  #
  # With Œ≤ = ùëñ = ‚àö-1 and Œæ = 1 + ùëñ
  #   1/2 * (c0 + c1, c1 - c0)

  when a.fromComplexExtension() and u == 1 and v == 1:
    let t = a.c0
    a.c0 += a.c1
    a.c1 -= t
    a.div2()
  else:
    let a0 = a.c0
    let a1 = a.c1
    const u2v2 = u*u - Beta*v*v # (u¬≤ - Œ≤v¬≤)
    # TODO can be precomputed (or precompute b/¬µ the twist coefficient)
    #      and use faster non-constant-time inversion in the VM
    var u2v2inv {.noInit.}: a.c0.typeof
    u2v2inv.fromUint(u2v2)
    u2v2inv.inv()

    a.c0.diff(u * a0, (Beta * v) * a1)
    a.c1.diff(u * a1, v * a0)
    a.c0 *= u2v2inv
    a.c1 *= u2v2inv

# ùîΩp6
# ----------------------------------------------------------------

type
  Fp4*[C: static Curve] = object
    c0*, c1*: Fp2[C]

  Fp6*[C: static Curve] = object
    c0*, c1*, c2*: Fp2[C]

  Œæ* = NonResidue
    # We call the non-residue Œæ on ùîΩp4/ùîΩp6 to avoid confusion
    # between non-residue
    # of different tower level

func `*`*(_: typedesc[Œæ], a: Fp2): Fp2 {.inline, noInit.} =
  ## Multiply an element of ùîΩp2 by the quadratic and cubic non-residue
  ## chosen to construct ùîΩp4/ùîΩp6
  # Yet another const tuple unpacking bug
  const u = Fp2.C.get_CNR_Fp2()[0]         # Quadratic & Cubic non-residue to construct ùîΩp4/ùîΩp6
  const v = Fp2.C.get_CNR_Fp2()[1]
  const Beta {.used.} = Fp2.C.get_QNR_Fp() # Quadratic non-residue to construct ùîΩp2
  # Œæ = u + v x
  # and x¬≤ = Œ≤
  #
  # (c0 + c1 x) (u + v x) => u c0 + (u c0 + u c1)x + v c1 x¬≤
  #                       => u c0 + Œ≤ v c1 + (v c0 + u c1) x

  # TODO: check code generated when Œæ = 1 + ùëñ
  #       The mul by constant are inline but
  #       since we don't have __restrict tag
  #       and we use arrays (which decay into pointer)
  #       the compiler might not elide the temporary
  when a.fromComplexExtension():
    result.c0.diff(u * a.c0, v * a.c1)
  else:
    result.c0.sum(u * a.c0, (Beta * v) * a.c1)
  result.c1.sum(v * a.c0, u * a.c1 )

func `*=`*(a: var Fp2, _: typedesc[Œæ]) {.inline.} =
  ## Multiply an element of ùîΩp2 by the quadratic non-residue
  ## chosen to construct ùîΩp6
  # Yet another const tuple unpacking bug
  const u = Fp2.C.get_CNR_Fp2()[0] # Cubic non-residue to construct ùîΩp6
  const v = Fp2.C.get_CNR_Fp2()[1]
  const Beta {.used.} = Fp2.C.get_QNR_Fp()  # Quadratic non-residue to construct ùîΩp2
  # Œæ = u + v x
  # and x¬≤ = Œ≤
  #
  # (c0 + c1 x) (u + v x) => u c0 + (u c0 + u c1)x + v c1 x¬≤
  #                       => u c0 + Œ≤ v c1 + (v c0 + u c1) x
  when a.fromComplexExtension() and u == 1 and v == 1:
    let t = a.c0
    a.c0 -= a.c1
    a.c1 += t
  else: # TODO: optim for inline
    a = Œæ * a

# ùîΩp12
# ----------------------------------------------------------------

type
  Fp12*[C: static Curve] = object
    c0*, c1*, c2*: Fp4[C]
    # c0*, c1*: Fp6[C]

  Œ≥ = NonResidue
    # We call the non-residue Œ≥ (Gamma) on ùîΩp6 to avoid confusion between non-residue
    # of different tower level

func `*`*(_: typedesc[Œ≥], a: Fp4): Fp4 {.noInit, inline.} =
  ## Multiply an element of ùîΩp4 by the sextic non-residue
  ## chosen to construct ùîΩp12
  result.c0 = Œæ * a.c1
  result.c1 = a.c0

func `*=`*(a: var Fp4, _: typedesc[Œ≥]) {.inline.} =
  a = Œ≥ * a

func `*`*(_: typedesc[Œ≥], a: Fp6): Fp6 {.noInit, inline.} =
  ## Multiply an element of ùîΩp6 by the cubic non-residue
  ## chosen to construct ùîΩp12
  ## For all curves Œ≥ = v with v the factor for ùîΩp6 coordinate
  ## and v¬≥ = Œæ
  ## (c0 + c1 v + c2 v¬≤) v => Œæ c2 + c0 v + c1 v¬≤
  result.c0 = Œæ * a.c2
  result.c1 = a.c0
  result.c2 = a.c1

func `*=`*(a: var Fp6, _: typedesc[Œ≥]) {.inline.} =
  a = Œ≥ * a

# Sparse functions
# ----------------------------------------------------------------

func `*=`*(a: var Fp2, b: Fp) =
  ## Multiply an element of Fp2 by an element of Fp
  a.c0 *= b
  a.c1 *= b

func mul_sparse_by_y0*[C: static Curve](r: var Fp4[C], a: Fp4[C], b: Fp2[C]) =
  ## Sparse multiplication of an Fp4 element
  ## with coordinates (a‚ÇÄ, a‚ÇÅ) by (b‚ÇÄ, 0)
  r.c0.prod(a.c0, b)
  r.c1.prod(a.c1, b)

func mul_sparse_by_0y*[C: static Curve](r: var Fp4[C], a: Fp4[C], b: Fp2[C]) =
  ## Sparse multiplication of an Fp4 element
  ## with coordinates (a‚ÇÄ, a‚ÇÅ) by (0, b‚ÇÅ)
  r.c0.prod(a.c1, b)
  r.c0 *= NonResidue
  r.c1.prod(a.c0, b)

func mul_sparse_by_0y0*[C: static Curve](r: var Fp6[C], a: Fp6[C], b: Fp2[C]) =
  ## Sparse multiplication of an Fp6 element
  ## with coordinates (a‚ÇÄ, a‚ÇÅ, a‚ÇÇ) by (0, b‚ÇÅ, 0)
  # TODO: make generic and move to tower_field_extensions

  # v0 = a0 b0 = 0
  # v1 = a1 b1
  # v2 = a2 b2 = 0
  #
  # r0 = Œæ ((a1 + a2) * (b1 + b2) - v1 - v2) + v0
  #    = Œæ (a1 b1 + a2 b1 - v1)
  #    = Œæ a2 b1
  # r1 = (a0 + a1) * (b0 + b1) - v0 - v1 + Œæ v2
  #    = a0 b1 + a1 b1 - v1
  #    = a0 b1
  # r2 = (a0 + a2) * (b0 + b2) - v0 - v2 + v1
  #    = v1
  #    = a1 b1

  r.c0.prod(a.c2, b)
  r.c0 *= Œæ
  r.c1.prod(a.c0, b)
  r.c2.prod(a.c1, b)
