# Constantine
# Copyright (c) 2018-2019    Status Research & Development GmbH
# Copyright (c) 2020-Present Mamy AndrÃ©-Ratsimbazafy
# Licensed and distributed under either of
#   * MIT license (license terms in the root directory or at http://opensource.org/licenses/MIT).
#   * Apache v2 license (license terms in the root directory or at http://www.apache.org/licenses/LICENSE-2.0).
# at your option. This file may not be copied, modified, or distributed except according to those terms.

# ############################################################
#
#        Quadratic Extension field over base field ğ”½p
#                        ğ”½p2 = ğ”½p[ğ‘–]
#
# ############################################################

# This implements a quadratic extension field over
# the base field ğ”½p:
#   ğ”½p2 = ğ”½p[x]
# with element A of coordinates (a0, a1) represented
# by a0 + a1 x
#
# The irreducible polynomial chosen is
#   xÂ² - Âµ with Âµ = -1
# i.e. ğ”½p2 = ğ”½p[ğ‘–], ğ‘– being the imaginary unit
#
# Consequently, for this file Fp2 to be valid
# -1 MUST not be a square in ğ”½p
#
# Âµ is also chosen to simplify multiplication and squaring
# => A(a0, a1) * B(b0, b1)
# => (a0 + a1 x) * (b0 + b1 x)
# => a0 b0 + (a0 b1 + a1 b0) x + a1 b1 xÂ²
# We need xÂ² to be as cheap as possible
#
# References
# [1] Constructing Tower Extensions for the implementation of Pairing-Based Cryptography\
#     Naomi Benger and Michael Scott, 2009\
#     https://eprint.iacr.org/2009/556
#
# [2] Choosing and generating parameters for low level pairing implementation on BN curves\
#     Sylvain Duquesne and Nadia El Mrabet and Safia Haloui and Franck Rondepierre, 2015\
#     https://eprint.iacr.org/2015/1212

# TODO: Clarify some assumptions about the prime p â‰¡ 3 (mod 4)

import
  ../arithmetic,
  ../config/curves,
  ./abelian_groups

type
  Fp2*[C: static Curve] = object
    ## Element of the extension field
    ## ğ”½p2 = ğ”½p[ğ‘–] of a prime p
    ##
    ## with coordinates (c0, c1) such as
    ## c0 + c1 ğ‘–
    ##
    ## This requires ğ‘–Â² = -1 to not
    ## be a square (mod p)
    c0*, c1*: Fp[C]

func square*(r: var Fp2, a: Fp2) =
  ## Return aÂ² in ğ”½p2 = ğ”½p[ğ‘–] in ``r``
  ## ``r`` is initialized/overwritten
  # (c0, c1)Â² => (c0 + c1ğ‘–)Â²
  #           => c0Â² + 2 c0 c1ğ‘– + (c1ğ‘–)Â²
  #           => c0Â²-c1Â² + 2 c0 c1ğ‘–
  #           => (c0Â²-c1Â², 2 c0 c1)
  #           or
  #           => ((c0-c1)(c0+c1), 2 c0 c1)
  #           => ((c0-c1)(c0-c1 + 2 c1), c0 * 2 c1)
  #
  # Costs (naive implementation)
  # - 1 Multiplication ğ”½p
  # - 2 Squarings ğ”½p
  # - 1 Doubling ğ”½p
  # - 1 Substraction ğ”½p
  # Stack: 4 * ModulusBitSize (4x ğ”½p element)
  #
  # Or (with 1 less Mul/Squaring at the cost of 1 addition and extra 2 ğ”½p stack space)
  #
  # - 2 Multiplications ğ”½p
  # - 1 Addition ğ”½p
  # - 1 Doubling ğ”½p
  # - 1 Substraction ğ”½p
  # Stack: 6 * ModulusBitSize (4x ğ”½p element + 1 named temporaries + 1 in-place multiplication temporary)
  # as in-place multiplications require a (shared) internal temporary

  var c0mc1 {.noInit.}: Fp[Fp2.C]
  c0mc1.diff(a.c0, a.c1) # c0mc1 = c0 - c1                            [1 Sub]
  r.c1.double(a.c1)      # result.c1 = 2 c1                           [1 Dbl, 1 Sub]
  r.c0.sum(c0mc1, r.c1)  # result.c0 = c0 - c1 + 2 c1                 [1 Add, 1 Dbl, 1 Sub]
  r.c0 *= c0mc1          # result.c0 = (c0 + c1)(c0 - c1) = c0Â² - c1Â² [1 Mul, 1 Add, 1 Dbl, 1 Sub] - ğ”½p temporary
  r.c1 *= a.c0           # result.c1 = 2 c1 c0                        [2 Mul, 1 Add, 1 Dbl, 1 Sub] - ğ”½p temporary

func prod*(r: var Fp2, a, b: Fp2) =
  ## Return a * b in ğ”½p2 = ğ”½p[ğ‘–] in ``r``
  ## ``r`` is initialized/overwritten
  # (a0, a1) (b0, b1) => (a0 + a1ğ‘–) (b0 + b1ğ‘–)
  #                   => (a0 b0 - a1 b1) + (a0 b1 + a1 b0) ğ‘–
  #
  # In Fp, multiplication has cost O(nÂ²) with n the number of limbs
  # while addition has cost O(3n) (n for addition, n for overflow, n for conditional substraction)
  # and substraction has cost O(2n) (n for substraction + underflow, n for conditional addition)
  #
  # Even for 256-bit primes, we are looking at always a minimum of n=5 limbs (with 2^63 words)
  # where addition/substraction are significantly cheaper than multiplication
  #
  # So we always reframe the imaginary part using Karatsuba approach to save a multiplication
  # (a0, a1) (b0, b1) => (a0 b0 - a1 b1) + ğ‘–( (a0 + a1)(b0 + b1) - a0 b0 - a1 b1 )
  #
  # Costs (naive implementation)
  # - 4 Multiplications ğ”½p
  # - 1 Addition ğ”½p
  # - 1 Substraction ğ”½p
  # Stack: 6 * ModulusBitSize (4x ğ”½p element + 2x named temporaries)
  #
  # Costs (Karatsuba)
  # - 3 Multiplications ğ”½p
  # - 3 Substraction ğ”½p (2 are fused)
  # - 2 Addition ğ”½p
  # Stack: 6 * ModulusBitSize (4x ğ”½p element + 2x named temporaries + 1 in-place multiplication temporary)
  var a0b0 {.noInit.}, a1b1 {.noInit.}: Fp[Fp2.C]
  a0b0.prod(a.c0, b.c0)                                         # [1 Mul]
  a1b1.prod(a.c1, b.c1)                                         # [2 Mul]

  r.c0.sum(a.c0, a.c1)  # r0 = (a0 + a1)                        # [2 Mul, 1 Add]
  r.c1.sum(b.c0, b.c1)  # r1 = (b0 + b1)                        # [2 Mul, 2 Add]
  r.c1 *= r.c0          # r1 = (b0 + b1)(a0 + a1)               # [3 Mul, 2 Add] - ğ”½p temporary

  r.c0.diff(a0b0, a1b1) # r0 = a0 b0 - a1 b1                    # [3 Mul, 2 Add, 1 Sub]
  r.c1 -= a0b0          # r1 = (b0 + b1)(a0 + a1) - a0b0        # [3 Mul, 2 Add, 2 Sub]
  r.c1 -= a1b1          # r1 = (b0 + b1)(a0 + a1) - a0b0 - a1b1 # [3 Mul, 2 Add, 3 Sub]

func inv*(r: var Fp2, a: Fp2) =
  ## Compute the modular multiplicative inverse of ``a``
  ## in ğ”½p2 = ğ”½p[ğ‘–]
  #
  # Algorithm: (the inverse exist if a != 0 which might cause constant-time issue)
  #
  # 1 / (a0 + a1 x) <=> (a0 - a1 x) / (a0 + a1 x)(a0 - a1 x)
  #                 <=> (a0 - a1 x) / (a0Â² - a1Â² xÂ²)
  # In our case ğ”½p2 = ğ”½p[ğ‘–], we have x = ğ‘–
  # So the inverse is (a0 - a1 ğ‘–) / (a0Â² + a1Â²)

  # [2 Sqr, 1 Add]
  var t0 {.noInit.}, t1 {.noInit.}: Fp[Fp2.C]
  t0.square(a.c0)
  t1.square(a.c1)
  t0 += t1             # t0 = a0Â² + a1Â² (the norm / squared magnitude of a)

  # [1 Inv, 2 Sqr, 1 Add]
  t0.inv(t0)           # t0 = 1 / (a0Â² + a1Â²)

  # [1 Inv, 2 Mul, 2 Sqr, 1 Add, 1 Neg]
  r.c0.prod(a.c0, t0)  # r0 = a0 / (a0Â² + a1Â²)
  t1.neg(t0)           # t0 = -1 / (a0Â² + a1Â²)
  r.c1.prod(a.c1, t1)  # r1 = -a1 / (a0Â² + a1Â²)
