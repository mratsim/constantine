# Constantine
# Copyright (c) 2018-2019    Status Research & Development GmbH
# Copyright (c) 2020-Present Mamy AndrÃ©-Ratsimbazafy
# Licensed and distributed under either of
#   * MIT license (license terms in the root directory or at http://opensource.org/licenses/MIT).
#   * Apache v2 license (license terms in the root directory or at http://www.apache.org/licenses/LICENSE-2.0).
# at your option. This file may not be copied, modified, or distributed except according to those terms.

# ############################################################
#
#      Quadratic Extension field over extension field ğ”½p6
#                      ğ”½p12 = ğ”½p6[âˆšÎ³]
#       with Î³ the cubic root of the non-residue of ğ”½p6
#
# ############################################################

# This implements a quadratic extension field over
#   ğ”½p12 = ğ”½p6[Î³]
# with Î³ the cubic root of the non-residue of ğ”½p6
# with element A of coordinates (a0, a1) represented
# by a0 + a1 Î³
#
# The irreducible polynomial chosen is
#   wÂ² - Î³
# with Î³ the cubic root of the non-residue of ğ”½p6
# I.e. if ğ”½p6 irreducible polynomial is
#   vÂ³ - Î¾ with Î¾ = 1+ğ‘–
# Î³ = v = âˆ›(1 + ğ‘–)
#
# Consequently, for this file ğ”½p12 to be valid
# âˆ›(1 + ğ‘–) MUST not be a square in ğ”½p6

import
  ../arithmetic,
  ../config/curves,
  ./abelian_groups,
  ./fp6_1_plus_i

type
  Fp12*[C: static Curve] = object
    ## Element of the extension field
    ## ğ”½p12 = ğ”½p6[Î³]
    ##
    ## I.e. if ğ”½p6 irreducible polynomial is
    ##   vÂ³ - Î¾ with Î¾ = 1+ğ‘–
    ## Î³ = v = âˆ›(1 + ğ‘–)
    ##
    ## with coordinates (c0, c1) such as
    ## c0 + c1 w
    c0*, c1*: Fp6[C]

  Gamma = object
    ## Î³ (Gamma) the quadratic non-residue of ğ”½p6
    ## Î³ = v with v the factor in for ğ”½p6 coordinate
    ## i.e. a point in ğ”½p6 as coordinates a0 + a1 v + a2 vÂ²

func `*`(_: typedesc[Gamma], a: Fp6): Fp6 {.noInit, inline.} =
  ## Multiply an element of ğ”½p6 by ğ”½p12 quadratic non-residue
  ## Conveniently Î³ = v with v the factor in for ğ”½p6 coordinate
  ## and vÂ³ = Î¾
  ## (c0 + c1 v + c2 vÂ²) v => Î¾ c2 + c0 v + c1 vÂ²
  discard

  result.c0 = a.c2 * Xi
  result.c1 = a.c0
  result.c2 = a.c1

template `*`(a: Fp6, _: typedesc[Gamma]): Fp6 =
  Gamma * a

func `*=`(a: var Fp6, _: typedesc[Gamma]) {.inline.} =
  a = Gamma * a

func square*(r: var Fp12, a: Fp12) =
  ## Return aÂ² in ``r``
  ## ``r`` is initialized/overwritten
  # (c0, c1)Â² => (c0 + c1 w)Â²
  #           => c0Â² + 2 c0 c1 w + c1Â²wÂ²
  #           => c0Â² + Î³ c1Â² + 2 c0 c1 w
  #           => (c0Â² + Î³ c1Â², 2 c0 c1)
  # We have 2 squarings and 1 multiplication in ğ”½p6
  # which are significantly more costly:
  # - 4 limbs like BN254:     multiplication is 20x slower than addition/substraction
  # - 6 limbs like BLS12-381: multiplication is 28x slower than addition/substraction
  #
  # We can save operations with one of the following expressions
  # of c0Â² + Î³ c1Â² and noticing that c0c1 is already computed for the "y" coordinate
  #
  # Alternative 1:
  #   c0Â² + Î³ c1Â² <=> (c0 - c1)(c0 - Î³ c1) + Î³ c0c1 + c0c1
  #
  # Alternative 2:
  #   c0Â² + Î³ c1Â² <=> (c0 + c1)(c0 + Î³ c1) - Î³ c0c1 - c0c1

  # r0 <- (c0 + c1)(c0 + Î³ c1)
  r.c0.sum(a.c0, a.c1)
  r.c1.sum(a.c0, Gamma * a.c1)
  r.c0 *= r.c1

  # r1 <- c0 c1
  r.c1.prod(a.c0, a.c1)

  # r0 = (c0 + c1)(c0 + Î³ c1) - Î³ c0c1 - c0c1
  r.c0 -= Gamma * r.c1
  r.c0 -= r.c1

  # r1 = 2 c0c1
  r.c1.double()

func prod*[C](r: var Fp12[C], a, b: Fp12[C]) =
  ## Returns r = a * b
  # r0 = a0 b0 + Î³ a1 b1
  # r1 = (a0 + a1) (b0 + b1) - a0 b0 - a1 b1 (Karatsuba)
  var t {.noInit.}: Fp6[C]

  # r1 <- (a0 + a1)(b0 + b1)
  r.c0.sum(a.c0, a.c1)
  t.sum(b.c0, b.c1)
  r.c1.prod(r.c0, t)

  # r0 <- a0 b0
  # r1 <- (a0 + a1)(b0 + b1) - a0 b0 - a1 b1
  r.c0.prod(a.c0, b.c0)
  t.prod(a.c1, b.c1)
  r.c1 -= r.c0
  r.c1 -= t

  # r0 <- a0 b0 + Î³ a1 b1
  r.c0 += Gamma * t

func inv*[C](r: var Fp12[C], a: Fp12[C]) =
  ## Compute the multiplicative inverse of ``a``
  #
  # Algorithm: (the inverse exist if a != 0 which might cause constant-time issue)
  #
  # 1 / (a0 + a1 w) <=> (a0 - a1 w) / (a0 + a1 w)(a0 - a1 w)
  #                 <=> (a0 - a1 w) / (a0Â² - a1Â² wÂ²)
  # In our case ğ”½p12 = ğ”½p6[Î³], we have wÂ² = Î³
  # So the inverse is (a0 - a1 w) / (a0Â² - Î³ a1Â²)

  # [2 Sqr, 1 Add]
  var v0 {.noInit.}, v1 {.noInit.}: Fp6[C]
  v0.square(a.c0)
  v1.square(a.c1)
  v0 -= Gamma * v1     # v0 = a0Â² - Î³ a1Â² (the norm / squared magnitude of a)

  # [1 Inv, 2 Sqr, 1 Add]
  v1.inv(v0)           # v1 = 1 / (a0Â² - Î³ a1Â²)

  # [1 Inv, 2 Mul, 2 Sqr, 1 Add, 1 Neg]
  r.c0.prod(a.c0, v1)  # r0 = a0 / (a0Â² - Î³ a1Â²)
  v0.neg(v1)           # v0 = -1 / (a0Â² - Î³ a1Â²)
  r.c1.prod(a.c1, v0)  # r1 = -a1 / (a0Â² - Î³ a1Â²)
