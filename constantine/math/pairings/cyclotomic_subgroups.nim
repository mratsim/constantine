# Constantine
# Copyright (c) 2018-2019    Status Research & Development GmbH
# Copyright (c) 2020-Present Mamy AndrÃ©-Ratsimbazafy
# Licensed and distributed under either of
#   * MIT license (license terms in the root directory or at http://opensource.org/licenses/MIT).
#   * Apache v2 license (license terms in the root directory or at http://www.apache.org/licenses/LICENSE-2.0).
# at your option. This file may not be copied, modified, or distributed except according to those terms.

import
  ../../platforms/abstractions,
  ../config/curves,
  ../arithmetic,
  ../extension_fields,
  ../isogenies/frobenius

# No exceptions allowed
{.push raises: [].}

# ############################################################
#
#                GÏ•â‚™, Cyclotomic subgroup of Fpâ¿
#         with GÏ•â‚™(p) = {Î± âˆˆ Fpâ¿ : Î±^Î¦â‚™(p) â‰¡ 1 (mod pâ¿)}
#
# ############################################################

# - Faster Squaring in the Cyclotomic Subgroup of Sixth Degree Extensions
#   Granger, Scott, 2009
#   https://eprint.iacr.org/2009/565.pdf
#
# - On the final exponentiation for calculating
#   pairings on ordinary elliptic curves
#   Scott, Benger, Charlemagne, Perez, Kachisa, 2008
#   https://eprint.iacr.org/2008/490.pdf

# ğ”½pâ¿ -> GÏ•â‚™ - Mapping to Cyclotomic group
# ----------------------------------------------------------------

func finalExpEasy*[C: static Curve](f: var Fp6[C]) {.meter.} =
  ## Easy part of the final exponentiation
  ##
  ## This maps the result of the Miller loop into the cyclotomic subgroup GÏ•â‚†
  ##
  ## We need to clear the Gâ‚œ cofactor to obtain
  ## an unique Gâ‚œ representation
  ## (reminder, Gâ‚œ is a multiplicative group hence we exponentiate by the cofactor)
  ##
  ## i.e. Fpâ¶ --> (fexp easy) --> GÏ•â‚† --> (fexp hard) --> Gâ‚œ
  ##
  ## The final exponentiation is fexp = f^((pâ¶ - 1) / r)
  ## It is separated into:
  ## f^((pâ¶ - 1) / r) = (pâ¶ - 1) / Ï•â‚†(p)  * Ï•â‚†(p) / r
  ##
  ## with the cyclotomic polynomial Ï•â‚†(p) = (pÂ²-p+1)
  ##
  ## With an embedding degree of 6, the easy part of final exponentiation is
  ##
  ##  f^(pÂ³âˆ’1)(p+1)
  ##
  ## And properties are
  ## 0. f^(pÂ³) â‰¡ conj(f) (mod pâ¶) for all f in Fp6
  ##
  ## After g = f^(pÂ³âˆ’1) the result g is on the cyclotomic subgroup
  ## 1. g^(-1) â‰¡ g^(pÂ³) (mod pâ¶)
  ## 2. Inversion can be done with conjugate
  ## 3. g is unitary, its norm |g| (the product of conjugates) is 1
  ## 4. Squaring has a fast compressed variant.
  #
  # Proofs:
  #
  # Fp6 can be defined as a quadratic extension over FpÂ³
  # with g = gâ‚€ + x gâ‚ with x a quadratic non-residue
  #
  # with q = pÂ³, qÂ² = pâ¶
  # The frobenius map f^q â‰¡ (fâ‚€ + x fâ‚)^q (mod qÂ²)
  #                       â‰¡ fâ‚€^q + x^q fâ‚^q (mod qÂ²)
  #                       â‰¡ fâ‚€ + x^q fâ‚ (mod qÂ²)
  #                       â‰¡ fâ‚€ - x fâ‚ (mod qÂ²)
  # hence
  # f^pÂ³ â‰¡ conj(f) (mod pâ¶)
  # Q.E.D. of (0)
  #
  # ----------------
  #
  # pâ¶ - 1 = (pÂ³âˆ’1)(pÂ³+1) = (pÂ³âˆ’1)(p+1)(pÂ²-p+1)
  # by Fermat's little theorem we have
  # f^(pâ¶ - 1) â‰¡ 1 (mod pâ¶)
  #
  # Hence f^(pÂ³âˆ’1)(pÂ³+1) â‰¡ 1 (mod pâ¶)
  #
  # We call g = f^(pÂ³âˆ’1) we have
  # g^(pÂ³+1) â‰¡ 1 (mod pâ¶) <=> g^(pÂ³) * g â‰¡ 1 (mod pâ¶)
  # hence g^(-1) â‰¡ g^(pÂ³) (mod pâ¶)
  # Q.E.D. of (1)
  #
  # --
  #
  # From (1) g^(-1) â‰¡ g^(pÂ³) (mod pâ¶) for g = f^(pÂ³âˆ’1)
  # and  (0) f^pÂ³ â‰¡ conj(f) (mod pâ¶)  for all f in fp12
  #
  # so g^(-1) â‰¡ conj(g) (mod pâ¶) for g = f^(pÂ³âˆ’1)
  # Q.E.D. of (2)
  #
  # --
  #
  # f^(pâ¶ - 1) â‰¡ 1 (mod pâ¶) by Fermat's Little Theorem
  # f^(pÂ³âˆ’1)(pÂ³+1) â‰¡ 1 (mod pâ¶)
  # g^(pÂ³+1) â‰¡ 1 (mod pâ¶)
  # g * g^pÂ³ â‰¡ 1 (mod pâ¶)
  # g * conj(g) â‰¡ 1 (mod pâ¶)
  # Q.E.D. of (3)
  var g {.noinit.}: typeof(f)
  g.inv(f)              # g = f^-1
  conj(f)               # f = f^pÂ³
  g *= f                # g = f^(pÂ³-1)
  f.frobenius_map(g)    # f = f^((pÂ³-1) p)
  f *= g                # f = f^((pÂ³-1) (p+1))

func finalExpEasy*[C: static Curve](f: var Fp12[C]) {.meter.} =
  ## Easy part of the final exponentiation
  ##
  ## This maps the result of the Miller loop into the cyclotomic subgroup GÏ•â‚â‚‚
  ##
  ## We need to clear the Gâ‚œ cofactor to obtain
  ## an unique Gâ‚œ representation
  ## (reminder, Gâ‚œ is a multiplicative group hence we exponentiate by the cofactor)
  ##
  ## i.e. FpÂ¹Â² --> (fexp easy) --> GÏ•â‚â‚‚ --> (fexp hard) --> Gâ‚œ
  ##
  ## The final exponentiation is fexp = f^((pÂ¹Â² - 1) / r)
  ## It is separated into:
  ## f^((pÂ¹Â² - 1) / r) = (pÂ¹Â² - 1) / Ï•â‚â‚‚(p)  * Ï•â‚â‚‚(p) / r
  ##
  ## with the cyclotomic polynomial Ï•â‚â‚‚(p) = (pâ´-pÂ²+1)
  ##
  ## With an embedding degree of 12, the easy part of final exponentiation is
  ##
  ##  f^(pâ¶âˆ’1)(pÂ²+1)
  ##
  ## And properties are
  ## 0. f^(pâ¶) â‰¡ conj(f) (mod pÂ¹Â²) for all f in Fp12
  ##
  ## After g = f^(pâ¶âˆ’1) the result g is on the cyclotomic subgroup
  ## 1. g^(-1) â‰¡ g^(pâ¶) (mod pÂ¹Â²)
  ## 2. Inversion can be done with conjugate
  ## 3. g is unitary, its norm |g| (the product of conjugates) is 1
  ## 4. Squaring has a fast compressed variant.
  #
  # Proofs:
  #
  # Fp12 can be defined as a quadratic extension over Fpâ¶
  # with g = gâ‚€ + x gâ‚ with x a quadratic non-residue
  #
  # with q = pâ¶, qÂ² = pÂ¹Â²
  # The frobenius map f^q â‰¡ (fâ‚€ + x fâ‚)^q (mod qÂ²)
  #                       â‰¡ fâ‚€^q + x^q fâ‚^q (mod qÂ²)
  #                       â‰¡ fâ‚€ + x^q fâ‚ (mod qÂ²)
  #                       â‰¡ fâ‚€ - x fâ‚ (mod qÂ²)
  # hence
  # f^pâ¶ â‰¡ conj(f) (mod pÂ¹Â²)
  # Q.E.D. of (0)
  #
  # ----------------
  #
  # pÂ¹Â² - 1 = (pâ¶âˆ’1)(pâ¶+1) = (pâ¶âˆ’1)(pÂ²+1)(pâ´-pÂ²+1)
  # by Fermat's little theorem we have
  # f^(pÂ¹Â² - 1) â‰¡ 1 (mod pÂ¹Â²)
  #
  # Hence f^(pâ¶âˆ’1)(pâ¶+1) â‰¡ 1 (mod pÂ¹Â²)
  #
  # We call g = f^(pâ¶âˆ’1) we have
  # g^(pâ¶+1) â‰¡ 1 (mod pÂ¹Â²) <=> g^(pâ¶) * g â‰¡ 1 (mod pÂ¹Â²)
  # hence g^(-1) â‰¡ g^(pâ¶) (mod pÂ¹Â²)
  # Q.E.D. of (1)
  #
  # --
  #
  # From (1) g^(-1) â‰¡ g^(pâ¶) (mod pÂ¹Â²) for g = f^(pâ¶âˆ’1)
  # and  (0) f^pâ¶ â‰¡ conj(f) (mod pÂ¹Â²)  for all f in fp12
  #
  # so g^(-1) â‰¡ conj(g) (mod pÂ¹Â²) for g = f^(pâ¶âˆ’1)
  # Q.E.D. of (2)
  #
  # --
  #
  # f^(pÂ¹Â² - 1) â‰¡ 1 (mod pÂ¹Â²) by Fermat's Little Theorem
  # f^(pâ¶âˆ’1)(pâ¶+1) â‰¡ 1 (mod pÂ¹Â²)
  # g^(pâ¶+1) â‰¡ 1 (mod pÂ¹Â²)
  # g * g^pâ¶ â‰¡ 1 (mod pÂ¹Â²)
  # g * conj(g) â‰¡ 1 (mod pÂ¹Â²)
  # Q.E.D. of (3)
  var g {.noinit.}: typeof(f)
  g.inv(f)              # g = f^-1
  conj(f)               # f = f^pâ¶
  g *= f                # g = f^(pâ¶-1)
  f.frobenius_map(g, 2) # f = f^((pâ¶-1) pÂ²)
  f *= g                # f = f^((pâ¶-1) (pÂ²+1))

# GÏ•â‚™ - Cyclotomic functions
# ----------------------------------------------------------------
# A cyclotomic group is a subgroup of Fpâ¿ defined by
#
# GÏ•â‚™(p) = {Î± âˆˆ Fpâ¿ : Î±^Î¦â‚™(p) = 1}
#
# The result of any pairing is in a cyclotomic subgroup

func cyclotomic_inv*[FT](a: var FT) {.meter.} =
  ## Fast inverse for a
  ## `a` MUST be in the cyclotomic subgroup
  ## consequently `a` MUST be unitary
  a.conj()

func cyclotomic_inv*[FT](r: var FT, a: FT) {.meter.} =
  ## Fast inverse for a
  ## `a` MUST be in the cyclotomic subgroup
  ## consequently `a` MUST be unitary
  r.conj(a)

func cyclotomic_square_cube_over_quad(r: var CubicExt, a: CubicExt) =
  ## Square `a` into `r`
  ## `a` MUST be in the cyclotomic subgroup
  ## consequently `a` MUST be unitary
  #
  # Faster Squaring in the Cyclotomic Subgroup of Sixth Degree Extensions
  # Granger, Scott, 2009
  # https://eprint.iacr.org/2009/565.pdf

  # Cubic extension field
  # A = 3aÂ² âˆ’ 2 Ì„a
  # B = 3 âˆši cÂ² + 2 Ì„b
  # C = 3bÂ² âˆ’ 2 Ì„c
  var v0{.noInit.}, v1{.noInit.}, v2{.noInit.}: typeof(a.c0)

  template a0: untyped = a.c0.c0
  template a1: untyped = a.c0.c1
  template a2: untyped = a.c1.c0
  template a3: untyped = a.c1.c1
  template a4: untyped = a.c2.c0
  template a5: untyped = a.c2.c1

  v0.square(a.c0)
  v1.square(a.c1)
  v2.square(a.c2)

  # From here on, r aliasing with a is only for the first operation
  # and only read/write the exact same coordinates

  # 3vâ‚€â‚€ - 2aâ‚€
  r.c0.c0.diff(v0.c0, a0)
  r.c0.c0.double()
  r.c0.c0 += v0.c0
  # 3vâ‚€â‚ + 2aâ‚
  r.c0.c1.sum(v0.c1, a1)
  r.c0.c1.double()
  r.c0.c1 += v0.c1
  # 3vâ‚â‚€ - 2aâ‚„
  r.c2.c0.diff(v1.c0, a4)
  r.c2.c0.double()
  r.c2.c0 += v1.c0
  # 3vâ‚â‚ + 2bâ‚…
  r.c2.c1.sum(v1.c1, a5)
  r.c2.c1.double()
  r.c2.c1 += v1.c1

  # Now B = 3 âˆši cÂ² + 2 Ì„b
  # beware of mul by non residue: âˆši vâ‚‚ = Î¾vâ‚‚â‚ + vâ‚‚â‚€âˆši

  # 3 (âˆši cÂ²)â‚€ + 2aâ‚‚
  v2.c1 *= NonResidue
  r.c1.c0.sum(v2.c1, a2)
  r.c1.c0.double()
  r.c1.c0 += v2.c1

  # 3 (âˆši cÂ²)â‚ - 2aâ‚ƒ
  r.c1.c1.diff(v2.c0, a3)
  r.c1.c1.double()
  r.c1.c1 += v2.c0

func cyclotomic_square_quad_over_cube[F](r: var QuadraticExt[F], a: QuadraticExt[F]) =
  ## Square `a` into `r`
  ## `a` MUST be in the cyclotomic subgroup
  ## consequently `a` MUST be unitary
  # Mapping between towering schemes
  # --------------------------------
  #
  # canonical <=> cubic over quadratic <=> quadratic over cubic
  #    câ‚€     <=>        aâ‚€            <=>            bâ‚€
  #    câ‚     <=>        aâ‚‚            <=>            bâ‚ƒ
  #    câ‚‚     <=>        aâ‚„            <=>            bâ‚
  #    câ‚ƒ     <=>        aâ‚            <=>            bâ‚„
  #    câ‚„     <=>        aâ‚ƒ            <=>            bâ‚‚
  #    câ‚…     <=>        aâ‚…            <=>            bâ‚…
  #
  # Hence, this formula for a cubic extension field
  #   A = 3aÂ² âˆ’ 2 Ì„a
  #   B = 3 âˆši cÂ² + 2 Ì„b
  #   C = 3bÂ² âˆ’ 2 Ì„c
  #
  # becomes
  #   A = (bâ‚€, bâ‚„) = 3(bâ‚€, bâ‚„)Â² - 2(bâ‚€,-bâ‚„)
  #   B = (bâ‚ƒ, bâ‚‚) = 3 âˆši(bâ‚, bâ‚…)Â² + 2(bâ‚ƒ, -bâ‚‚)
  #   C = (bâ‚, bâ‚…) = 3(bâ‚ƒ, bâ‚‚)Â² - 2(bâ‚, -bâ‚…)
  #
  # with
  #   vâ‚€ = (bâ‚€, bâ‚„) = (a.c0.c0, a.c1.c1)
  #   vâ‚ = (bâ‚ƒ, bâ‚‚) = (a.c1.c0, a.c0.c2)
  #   vâ‚‚ = (bâ‚, bâ‚…) = (a.c0.c1, a.c1.c2)
  var v0{.noInit.}, v1{.noInit.}, v2{.noInit.}: QuadraticExt[typeof(r.c0.c0)]

  template b0: untyped = a.c0.c0
  template b1: untyped = a.c0.c1
  template b2: untyped = a.c0.c2
  template b3: untyped = a.c1.c0
  template b4: untyped = a.c1.c1
  template b5: untyped = a.c1.c2

  v0.square_disjoint(b0, b4)
  v1.square_disjoint(b3, b2)
  v2.square_disjoint(b1, b5)

  # From here on, r aliasing with a is only for the first operation
  # and only read/write the exact same coordinates

  # 3vâ‚€â‚€ - 2bâ‚€
  r.c0.c0.diff(v0.c0, b0)
  r.c0.c0.double()
  r.c0.c0 += v0.c0
  # 3vâ‚â‚€ - 2bâ‚
  r.c0.c1.diff(v1.c0, b1)
  r.c0.c1.double()
  r.c0.c1 += v1.c0
  # 3vâ‚€â‚ + 2bâ‚„
  r.c1.c1.sum(v0.c1, b4)
  r.c1.c1.double()
  r.c1.c1 += v0.c1
  # 3vâ‚â‚ + 2bâ‚…
  r.c1.c2.sum(v1.c1, b5)
  r.c1.c2.double()
  r.c1.c2 += v1.c1

  # Now B = (bâ‚ƒ, bâ‚‚) = 3 âˆši(bâ‚, bâ‚…)Â² + 2(bâ‚ƒ, -bâ‚‚)
  # beware of mul by non residue: âˆši vâ‚‚ = Î¾vâ‚‚â‚ + vâ‚‚â‚€âˆši

  # 3 (âˆši (bâ‚, bâ‚…)Â²)â‚€ + 2bâ‚ƒ
  v2.c1 *= NonResidue
  r.c1.c0.sum(v2.c1, b3)
  r.c1.c0.double()
  r.c1.c0 += v2.c1

  # 3 (âˆši (bâ‚, bâ‚…)Â²)â‚ - 2bâ‚ƒ
  r.c0.c2.diff(v2.c0, b2)
  r.c0.c2.double()
  r.c0.c2 += v2.c0

func cyclotomic_square*[FT](r: var FT, a: FT) {.inline, meter.} =
  ## Square `a` into `r`
  ## `a` MUST be in the cyclotomic subgroup
  ## consequently `a` MUST be unitary
  #
  # Faster Squaring in the Cyclotomic Subgroup of Sixth Degree Extensions
  # Granger, Scott, 2009
  # https://eprint.iacr.org/2009/565.pdf
  when a is CubicExt:
    r.cyclotomic_square_cube_over_quad(a)
  else:
    r.cyclotomic_square_quad_over_cube(a)

func cyclotomic_square*[FT](a: var FT) {.inline.} =
  ## Square `a` into `r`
  ## `a` MUST be in the cyclotomic subgroup
  ## consequently `a` MUST be unitary
  #
  # Faster Squaring in the Cyclotomic Subgroup of Sixth Degree Extensions
  # Granger, Scott, 2009
  # https://eprint.iacr.org/2009/565.pdf
  a.cyclotomic_square(a)

func cycl_sqr_repeated*[FT](f: var FT, num: int) {.inline, meter.} =
  ## Repeated cyclotomic squarings
  for _ in 0 ..< num:
    f.cyclotomic_square()

func cycl_sqr_repeated*[FT](r: var FT, a: FT, num: int) {.inline, meter.} =
  ## Repeated cyclotomic squarings
  r.cyclotomic_square(a)
  for _ in 1 ..< num:
    r.cyclotomic_square()

func cyclotomic_exp*[FT](r: var FT, a: FT, exponent: static BigInt, invert: bool) {.meter.} =
  ## Assumes public exponent
  var na {.noInit.}: FT
  na.cyclotomic_inv(a)

  r.setOne()
  var init = false
  for bit in recoding_l2r_signed_vartime(exponent):
    if init:
      r.cyclotomic_square()
    if bit == 1:
      if not init:
        r = a
        init = true
      else:
        r *= a
    elif bit == -1:
      if not init:
        r = na
        init = true
      else:
        r *= na

  if invert:
    r.cyclotomic_inv()

func isInCyclotomicSubgroup*[C](a: Fp6[C]): SecretBool =
  ## Check if a âˆˆ Fpâ¿: a^Î¦â‚™(p) = 1
  ## Î¦â‚†(p) = pÂ²-p+1
  var t{.noInit.}, p{.noInit.}: Fp6[C]

  t.frobenius_map(a, 2)  # a^(pÂ²)
  t *= a                 # a^(pÂ²+1)
  p.frobenius_map(a)     # a^(p)

  return t == p and not a.isZero()

func isInCyclotomicSubgroup*[C](a: Fp12[C]): SecretBool =
  ## Check if a âˆˆ Fpâ¿: a^Î¦â‚™(p) = 1
  ## Î¦â‚â‚‚(p) = pâ´-pÂ²+1
  var t{.noInit.}, p2{.noInit.}: Fp12[C]

  p2.frobenius_map(a, 2) # a^(pÂ²)
  t.frobenius_map(p2, 2) # a^(pâ´)
  t *= a                 # a^(pâ´+1)

  return t == p2 and not a.isZero()

# ############################################################
#
#                Compressed representations
#
# ############################################################
#
# The special structure of cyclotomic subgroup allows compression that
# can lead to faster exponentiation:
#
# - Compression in Finite Fields and Torus-Based Cryptography
#   Rubin and Silverberg, 2003
#   https://citeseerx.ist.psu.edu/viewdoc/download?doi=10.1.1.90.8087&rep=rep1&type=pdf
#
# - Squaring in Cyclotomic Subgroup
#   Karabina, 2012
#   https://www.ams.org/journals/mcom/2013-82-281/S0025-5718-2012-02625-1/S0025-5718-2012-02625-1.pdf
#
# Karabina's formula Gâ‚‚â‚ƒâ‚„â‚… has the best squaring/decompression cost ratio.
# From a sextic tower FpáµáŸâ¶ -> FpáµáŸÂ³ -> Fpáµ with quadratic-non-residue u and cubic non-residue v
# Î± = (aâ‚€+aâ‚u) + (bâ‚€+bâ‚u)v + (câ‚€+câ‚u)vÂ²
# Compressed Î±: C(Î±) = (bâ‚€+bâ‚u)v + (câ‚€+câ‚u)vÂ² = (gâ‚‚+gâ‚ƒu)v + (gâ‚„+gâ‚…u)vÂ²
# C(Î±)Â² = (hâ‚‚+hâ‚ƒu)v + (hâ‚„+hâ‚…u)vÂ²
#  with hâ‚‚ = 2(gâ‚‚ + 3Î¾gâ‚„gâ‚…)
#       hâ‚ƒ = 3((gâ‚„+gâ‚…)(gâ‚„+Î¾gâ‚…) - (Î¾+1)gâ‚„gâ‚…) - 2gâ‚ƒ
#       hâ‚„ = 3((gâ‚‚+gâ‚ƒ)(gâ‚‚+Î¾gâ‚ƒ) - (Î¾+1)gâ‚‚gâ‚ƒ) - 2gâ‚„
#       hâ‚… = 2(gâ‚… + 3(gâ‚‚+gâ‚ƒ)(gâ‚‚+Î¾gâ‚ƒ))
#
# For decompression we can recompute the missing coefficients
# if gâ‚‚ != 0
#       gâ‚ = (gâ‚…Â²Î¾ + 3gâ‚„Â² - 2gâ‚ƒ)/4gâ‚‚       gâ‚€ = (2gâ‚Â² + gâ‚‚gâ‚… - 3gâ‚ƒgâ‚„)Î¾ + 1
# if gâ‚‚ == 0
#       gâ‚ = 2gâ‚„gâ‚…/gâ‚ƒ                      gâ‚€ = (2gâ‚Â²        - 3gâ‚ƒgâ‚„)Î¾ + 1

type G2345*[F] = object
  ## Compressed representation of cyclotomic subgroup element of a sextic extension
  ## (0 + 0u) + (gâ‚‚+gâ‚ƒu)v + (gâ‚„+gâ‚…u)vÂ²
  g2, g3, g4, g5: F

func cyclotomic_square_compressed*[F](g: var G2345[F]) =
  ## Karabina's compressed squaring
  ## for sextic extension fields
  # C(Î±)Â² = (hâ‚‚+hâ‚ƒu)v + (hâ‚„+hâ‚…u)vÂ²
  # with
  #       hâ‚‚ = 2(gâ‚‚ + 3Î¾gâ‚„gâ‚…)
  #       hâ‚ƒ = 3((gâ‚„+gâ‚…)(gâ‚„+Î¾gâ‚…) - (Î¾+1)gâ‚„gâ‚…) - 2gâ‚ƒ
  #       hâ‚„ = 3((gâ‚‚+gâ‚ƒ)(gâ‚‚+Î¾gâ‚ƒ) - (Î¾+1)gâ‚‚gâ‚ƒ) - 2gâ‚„
  #       hâ‚… = 2(gâ‚… + 3(gâ‚‚+gâ‚ƒ)(gâ‚‚+Î¾gâ‚ƒ))
  # (4 mul, theorem 3.2 p561)
  #
  # or
  #       hâ‚‚ = 2gâ‚‚ + 6Î¾gâ‚„gâ‚…
  #       hâ‚ƒ = 3gâ‚„Â² + 3gâ‚…Â²Î¾ - 2gâ‚ƒ
  #       hâ‚„ = 3gâ‚‚Â² + 3gâ‚ƒÂ²Î¾ - 2gâ‚„
  #       hâ‚… = 2gâ‚… + 6gâ‚‚gâ‚ƒ
  # (2 mul, 4 sqr, section 5.3 p567)
  #
  # or
  #       hâ‚‚ = 2gâ‚‚ + 3Î¾((gâ‚„+gâ‚…)Â²-gâ‚„Â²-gâ‚…Â²)
  #       hâ‚ƒ = 3(gâ‚„Â² + gâ‚…Â²Î¾) - 2gâ‚ƒ
  #       hâ‚„ = 3(gâ‚‚Â² + gâ‚ƒÂ²Î¾) - 2gâ‚„
  #       hâ‚… = 2gâ‚… + 3 ((gâ‚‚+gâ‚ƒ)Â²-gâ‚‚Â²-gâ‚ƒÂ²)
  # (6 sqr)
  #
  # or with quadratic arithmetic
  #   (hâ‚‚+hâ‚ƒu) = 3u(gâ‚„+gâ‚…u)Â² + 2(gâ‚‚-gâ‚ƒu)
  #   (hâ‚„+hâ‚…u) = 3 (gâ‚‚+gâ‚ƒu)Â² - 2(gâ‚„-gâ‚…u)
  # (2x2mul or 2x3sqr, section 5.3 p567)
  var g2g3 {.noInit.} = QuadraticExt[F](coords:[g.g2, g.g3])
  var g4g5 {.noInit.} = QuadraticExt[F](coords:[g.g4, g.g5])
  var h2h3 {.noInit.}, h4h5 {.noInit.}: QuadraticExt[F]

  h2h3.square(g4g5)
  h2h3 *= NonResidue
  h2h3 *= 3

  h4h5.square(g2g3)
  h4h5 *= 3

  g2g3.double()
  g4g5.double()

  g.g2.sum(h2h3.c0, g2g3.c0)
  g.g3.diff(h2h3.c1, g2g3.c1)
  g.g4.diff(h4h5.c0, g4g5.c0)
  g.g5.sum(h4h5.c1, g4g5.c1)

func recover_g1*[F](g1_num, g1_den: var F, g: G2345[F]) =
  ## Compute gâ‚ from coordinates gâ‚‚gâ‚ƒgâ‚„gâ‚…
  ## of a cyclotomic subgroup element of a sextic extension field
  # if gâ‚‚ != 0
  #   gâ‚ = (gâ‚…Â²Î¾ + 3gâ‚„Â² - 2gâ‚ƒ)/4gâ‚‚
  # if gâ‚‚ == 0
  #   gâ‚ = 2gâ‚„gâ‚…/gâ‚ƒ
  #
  # Theorem 3.1, this is well-defined for all
  # g in GÏ•â‚™ \ {1}
  # if gâ‚‚=gâ‚ƒ=0 then gâ‚„=gâ‚…=0 as well
  # and gâ‚€ = 1
  let g2NonZero = not g.g2.isZero()
  var t{.noInit.}: F

  g1_num = g.g4
  t = g.g5
  t.ccopy(g.g4, g2NonZero)
  t *= g1_num                     #  gâ‚„Â²              or  gâ‚„gâ‚…
  g1_num = t
  g1_num.csub(g.g3, g2NonZero)    #  gâ‚„Â²- gâ‚ƒ
  g1_num.double()                 # 2gâ‚„Â²-2gâ‚ƒ          or 2gâ‚„gâ‚…
  g1_num.cadd(t, g2NonZero)       # 3gâ‚„Â²-2gâ‚ƒ          or 2gâ‚„gâ‚…

  t.square(g.g5)
  t *= NonResidue
  g1_num.cadd(t, g2NonZero)       # gâ‚…Â²Î¾ + 3gâ‚„Â² - 2gâ‚ƒ or 2gâ‚„gâ‚…

  t.prod(g.g2, 4)
  g1_den = g.g3
  g1_den.ccopy(t, g2NonZero)      # 4gâ‚‚ or gâ‚ƒ

func batch_ratio_g1s*[N: static int, F](
       dst: var array[N, F],
       src: array[N, tuple[g1_num, g1_den: F]]) =
  ## Batch inversion of gâ‚
  ## returns g1_numáµ¢/g1_denáµ¢
  ## This requires that all g1_den != 0 or all g1_den == 0
  ## which is the case if this is used to implement
  ## exponentiation in cyclotomic subgroup.

  # Algorithm: Montgomery's batch inversion
  # - Speeding the Pollard and Elliptic Curve Methods of Factorization
  #   Section 10.3.1
  #   Peter L. Montgomery
  #   https://www.ams.org/journals/mcom/1987-48-177/S0025-5718-1987-0866113-7/S0025-5718-1987-0866113-7.pdf
  # - Modern Computer Arithmetic
  #   Section 2.5.1 Several inversions at once
  #   Richard P. Brent and Paul Zimmermann
  #   https://members.loria.fr/PZimmermann/mca/mca-cup-0.5.9.pdf

  dst[0] = src[0].g1_den
  for i in 1 ..< N:
    dst[i].prod(dst[i-1], src[i].g1_den)

  var accInv{.noInit.}: F
  accInv.inv(dst[N-1])

  for i in countdown(N-1, 1):
    # Compute inverse
    dst[i].prod(accInv, dst[i-1])
    # Apply it
    dst[i] *= src[i].g1_num
    # Next iteration
    accInv *= src[i].g1_den

  dst[0].prod(accInv, src[0].g1_num)

func recover_g0*[F](
       g0: var F, g1: F,
       g: G2345[F]) =
  ## Compute gâ‚€ from coordinates gâ‚gâ‚‚gâ‚ƒgâ‚„gâ‚…
  ## of a cyclotomic subgroup element of a sextic extension field
  var t{.noInit.}: F

  t.square(g1)
  g0.prod(g.g3, g.g4)
  t -= g0
  t.double()
  t -= g0
  g0.prod(g.g2, g.g5)
  t += g0
  g0.prod(t, NonResidue)
  t.setOne()
  g0 += t

func fromFpk*[Fpkdiv6, Fpk](
       g: var G2345[Fpkdiv6],
       a: Fpk) =
  ## Convert from a sextic extension to the Karabina gâ‚‚â‚ƒâ‚„â‚…
  ## representation.

  # GT representations isomorphisms
  # ===============================
  #
  # Given a sextic twist, we can express all elements in terms of z = SNRÂ¹áŸâ¶
  #
  # The canonical direct sextic representation uses coefficients
  #
  #    câ‚€ + câ‚ z + câ‚‚ zÂ² + câ‚ƒ zÂ³ + câ‚„ zâ´ + câ‚… zâµ
  #
  # with z = SNRÂ¹áŸâ¶
  #
  # The cubic over quadratic towering
  # ---------------------------------
  #
  #   (aâ‚€ + aâ‚ u) + (aâ‚‚ + aâ‚ƒu) v + (aâ‚„ + aâ‚…u) vÂ²
  #
  # with u = (SNR)Â¹áŸÂ² and v = z = uÂ¹áŸÂ³ = (SNR)Â¹áŸâ¶
  #
  # The quadratic over cubic towering
  # ---------------------------------
  #
  #   (bâ‚€ + bâ‚x + bâ‚‚xÂ²) + (bâ‚ƒ + bâ‚„x + bâ‚…xÂ²)y
  #
  # with x = (SNR)Â¹áŸÂ³ and y = z = xÂ¹áŸÂ² = (SNR)Â¹áŸâ¶
  #
  # Mapping between towering schemes
  # --------------------------------
  #
  # gâ‚‚â‚ƒâ‚„â‚… uses the cubic over quadratic representation hence:
  #
  #   câ‚€ <=> aâ‚€ <=> bâ‚€ <=> gâ‚€
  #   câ‚ <=> aâ‚‚ <=> bâ‚ƒ <=> gâ‚‚
  #   câ‚‚ <=> aâ‚„ <=> bâ‚ <=> gâ‚„
  #   câ‚ƒ <=> aâ‚ <=> bâ‚„ <=> gâ‚
  #   câ‚„ <=> aâ‚ƒ <=> bâ‚‚ <=> gâ‚ƒ
  #   câ‚… <=> aâ‚… <=> bâ‚… <=> gâ‚…
  #
  # See also chapter 6.4
  # - Multiplication and Squaring on Pairing-Friendly Fields
  #   Augusto Jun Devegili and Colm Ã“ hÃ‰igeartaigh and Michael Scott and Ricardo Dahab, 2006
  #   https://eprint.iacr.org/2006/471

  when a is CubicExt:
    when a.c0 is QuadraticExt:
      g.g2 = a.c1.c0
      g.g3 = a.c1.c1
      g.g4 = a.c2.c0
      g.g5 = a.c2.c1
    else:
      {.error: "a must be a sextic extension field".}
  elif a is QuadraticExt:
    when a.c0 is CubicExt:
      g.g2 = a.c1.c0
      g.g3 = a.c0.c2
      g.g4 = a.c0.c1
      g.g5 = a.c1.c2
    else:
      {.error: "a must be a sextic extension field".}
  else:
    {.error: "ğ”½páµáŸâ¶ -> ğ”½páµ towering (direct sextic) is not implemented.".}

func asFpk*[Fpkdiv6, Fpk](
       a: var Fpk,
       g0, g1: Fpkdiv6,
       g: G2345[Fpkdiv6]) =
  ## Convert from a sextic extension to the Karabina gâ‚‚â‚ƒâ‚„â‚…
  ## representation.
  when a is CubicExt:
    when a.c0 is QuadraticExt:
      a.c0.c0 = g0
      a.c0.c1 = g1
      a.c1.c0 = g.g2
      a.c1.c1 = g.g3
      a.c2.c0 = g.g4
      a.c2.c1 = g.g5
    else:
      {.error: "a must be a sextic extension field".}
  elif a is QuadraticExt:
    when a.c0 is CubicExt:
      a.c0.c0 = g0
      a.c0.c1 = g.g4
      a.c0.c2 = g.g3
      a.c1.c0 = g.g2
      a.c1.c1 = g1
      a.c1.c2 = g.g5
    else:
      {.error: "a must be a sextic extension field".}
  else:
    {.error: "ğ”½páµáŸâ¶ -> ğ”½páµ towering (direct sextic) is not implemented.".}

func cyclotomic_exp_compressed*[N: static int, Fpk](
       r: var Fpk, a: Fpk,
       squarings: static array[N, int]) =
  ## Exponentiation on the cyclotomic subgroup
  ## via compressed repeated squarings
  ## Exponentiation is done least-signigicant bits first
  ## `squarings` represents the number of squarings
  ## to do before the next multiplication.

  type Fpkdiv6 = typeof(a.c0.c0)

  var gs {.noInit.}: array[N, G2345[Fpkdiv6]]

  var g {.noInit.}: G2345[Fpkdiv6]
  g.fromFpk(a)

  # Compressed squarings
  for i in 0 ..< N:
    for j in 0 ..< squarings[i]:
      g.cyclotomic_square_compressed()
    gs[i] = g

  # Batch decompress
  var g1s_ratio {.noInit.}: array[N, tuple[g1_num, g1_den: Fpkdiv6]]
  for i in 0 ..< N:
    recover_g1(g1s_ratio[i].g1_num, g1s_ratio[i].g1_den, gs[i])

  var g1s {.noInit.}: array[N, Fpkdiv6]
  g1s.batch_ratio_g1s(g1s_ratio)

  var g0s {.noInit.}: array[N, Fpkdiv6]
  for i in 0 ..< N:
    g0s[i].recover_g0(g1s[i], gs[i])

  r.asFpk(g0s[0], g1s[0], gs[0])
  for i in 1 ..< N:
    var t {.noInit.}: Fpk
    t.asFpk(g0s[i], g1s[i], gs[i])
    r *= t

func cyclotomic_exp_compressed*[N: static int, Fpk](
       r, accumSquarings: var Fpk, a: Fpk,
       squarings: static array[N, int]) =
  ## Exponentiation on the cyclotomic subgroup
  ## via compressed repeated squarings
  ## Exponentiation is done least-signigicant bits first
  ## `squarings` represents the number of squarings
  ## to do before the next multiplication.
  ##
  ## `accumSquarings` stores the accumulated squarings so far
  ## iff N != 1

  type Fpkdiv6 = typeof(a.c0.c0)

  var gs {.noInit.}: array[N, G2345[Fpkdiv6]]

  var g {.noInit.}: G2345[Fpkdiv6]
  g.fromFpk(a)

  # Compressed squarings
  for i in 0 ..< N:
    for j in 0 ..< squarings[i]:
      g.cyclotomic_square_compressed()
    gs[i] = g

  # Batch decompress
  var g1s_ratio {.noInit.}: array[N, tuple[g1_num, g1_den: Fpkdiv6]]
  for i in 0 ..< N:
    recover_g1(g1s_ratio[i].g1_num, g1s_ratio[i].g1_den, gs[i])

  var g1s {.noInit.}: array[N, Fpkdiv6]
  g1s.batch_ratio_g1s(g1s_ratio)

  var g0s {.noInit.}: array[N, Fpkdiv6]
  for i in 0 ..< N:
    g0s[i].recover_g0(g1s[i], gs[i])

  r.asFpk(g0s[0], g1s[0], gs[0])
  for i in 1 ..< N:
    var t {.noInit.}: Fpk
    t.asFpk(g0s[i], g1s[i], gs[i])
    r *= t

    if i+1 == N:
      accumSquarings = t