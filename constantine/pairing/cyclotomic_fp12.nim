# Constantine
# Copyright (c) 2018-2019    Status Research & Development GmbH
# Copyright (c) 2020-Present Mamy AndrÃ©-Ratsimbazafy
# Licensed and distributed under either of
#   * MIT license (license terms in the root directory or at http://opensource.org/licenses/MIT).
#   * Apache v2 license (license terms in the root directory or at http://www.apache.org/licenses/LICENSE-2.0).
# at your option. This file may not be copied, modified, or distributed except according to those terms.

import
  ../primitives,
  ../config/curves,
  ../arithmetic,
  ../towers,
  ../isogeny/frobenius

# ############################################################
#
#               GÏ•â‚â‚‚, Cyclotomic subgroup of Fp12
#         with GÎ¦â‚™(p) = {Î± âˆˆ Fpâ¿ : Î±^Î¦â‚™(p) â‰¡ 1 (mod pâ¿)}
#
# ############################################################

# - Faster Squaring in the Cyclotomic Subgroup of Sixth Degree Extensions
#   Granger, Scott, 2009
#   https://eprint.iacr.org/2009/565.pdf
#
# - On the final exponentiation for calculating
#   pairings on ordinary elliptic curves
#   Scott, Benger, Charlemagne, Perez, Kachisa, 2008
#   https://eprint.iacr.org/2008/490.pdf

# ğ”½p12 -> GÏ•â‚â‚‚ - Mapping to Cyclotomic group
# ----------------------------------------------------------------
func finalExpEasy*[C: static Curve](f: var Fp12[C]) =
  ## Easy part of the final exponentiation
  ##
  ## This maps the result of the Miller loop into the cyclotomic subgroup GÏ•â‚â‚‚
  ##
  ## We need to clear the Gâ‚œ cofactor to obtain
  ## an unique Gâ‚œ representation
  ## (reminder, Gâ‚œ is a multiplicative group hence we exponentiate by the cofactor)
  ##
  ## i.e. Fp^12 --> (fexp easy) --> GÏ•â‚â‚‚ --> (fexp hard) --> Gâ‚œ
  ##
  ## The final exponentiation is fexp = f^((p^12 - 1) / r)
  ## It is separated into:
  ## f^((p^12 - 1) / r) = (p^12 - 1) / Ï•â‚â‚‚(p)  * Ï•â‚â‚‚(p) / r
  ##
  ## with the cyclotomic polynomial Ï•â‚â‚‚(p) = (pâ´-pÂ²+1)
  ##
  ## With an embedding degree of 12, the easy part of final exponentiation is
  ##
  ##  f^(pâ¶âˆ’1)(pÂ²+1)
  ##
  ## And properties are
  ## 0. f^(pâ¶) â‰¡ conj(f) (mod p^12) for all f in Fp12
  ##
  ## After g = f^(pâ¶âˆ’1) the result g is on the cyclotomic subgroup
  ## 1. g^(-1) â‰¡ g^(pâ¶) (mod p^12)
  ## 2. Inversion can be done with conjugate
  ## 3. g is unitary, its norm |g| (the product of conjugates) is 1
  ## 4. Squaring has a fast compressed variant.
  #
  # Proofs:
  #
  # Fp12 can be defined as a quadratic extension over Fpâ¶
  # with g = gâ‚€ + x gâ‚ with x a quadratic non-residue
  #
  # with q = pâ¶
  # The frobenius map f^q â‰¡ (fâ‚€ + x fâ‚)^q (mod qÂ²)
  #                       â‰¡ fâ‚€^q + x^q fâ‚^q (mod qÂ²)
  #                       â‰¡ fâ‚€ + x^q fâ‚ (mod qÂ²)
  #                       â‰¡ fâ‚€ - x fâ‚ (mod qÂ²)
  # hence
  # f^pâ¶ â‰¡ conj(f) (mod p^12)
  # Q.E.D. of (0)
  #
  # ----------------
  #
  # p^12 - 1 = (pâ¶âˆ’1)(pâ¶+1) = (pâ¶âˆ’1)(pÂ²+1)(pâ´-pÂ²+1)
  # by Fermat's little theorem we have
  # f^(p^12 - 1) â‰¡ 1 (mod p^12)
  #
  # Hence f^(pâ¶âˆ’1)(pâ¶+1) â‰¡ 1 (mod p^12)
  #
  # We call g = f^(pâ¶âˆ’1) we have
  # g^(pâ¶+1) â‰¡ 1 (mod p^12) <=> g^(pâ¶) * g â‰¡ 1 (mod p^12)
  # hence g^(-1) â‰¡ g^(pâ¶) (mod p^12)
  # Q.E.D. of (1)
  #
  # --
  #
  # From (1) g^(-1) â‰¡ g^(pâ¶) (mod p^12) for g = f^(pâ¶âˆ’1)
  # and  (0) f^pâ¶ â‰¡ conj(f) (mod p^12)  for all f in fp12
  #
  # so g^(-1) â‰¡ conj(g) (mod p^12) for g = f^(pâ¶âˆ’1)
  # Q.E.D. of (2)
  #
  # --
  #
  # f^(p^12 - 1) â‰¡ 1 (mod p^12) by Fermat's Little Theorem
  # f^(pâ¶âˆ’1)(pâ¶+1) â‰¡ 1 (mod p^12)
  # g^(pâ¶+1) â‰¡ 1 (mod p^12)
  # g * g^pâ¶ â‰¡ 1 (mod p^12)
  # g * conj(g) â‰¡ 1 (mod p^12)
  # Q.E.D. of (3)
  var g {.noinit.}: typeof(f)
  g.inv(f)              # g = f^-1
  conj(f)               # f = f^pâ¶
  g *= f                # g = f^(pâ¶-1)
  f.frobenius_map(g, 2) # f = f^((pâ¶-1) pÂ²)
  f *= g                # f = f^((pâ¶-1) (pÂ²+1))

# GÏ•â‚â‚‚ - Cyclotomic functions
# ----------------------------------------------------------------
# A cyclotomic group is a subgroup of Fp^n defined by
#
# GÎ¦â‚™(p) = {Î± âˆˆ Fpâ¿ : Î±^Î¦â‚™(p) = 1}
#
# The result of any pairing is in a cyclotomic subgroup

func cyclotomic_inv*(a: var Fp12) =
  ## Fast inverse for a
  ## `a` MUST be in the cyclotomic subgroup
  ## consequently `a` MUST be unitary
  a.conj()

func cyclotomic_inv*(r: var Fp12, a: Fp12) =
  ## Fast inverse for a
  ## `a` MUST be in the cyclotomic subgroup
  ## consequently `a` MUST be unitary
  r.conj(a)

func cyclotomic_square*[C](r: var Fp12[C], a: Fp12[C]) =
  ## Square `a` into `r`
  ## `a` MUST be in the cyclotomic subgroup
  ## consequently `a` MUST be unitary
  #
  # Faster Squaring in the Cyclotomic Subgroup of Sixth Degree Extensions
  # Granger, Scott, 2009
  # https://eprint.iacr.org/2009/565.pdf

  when a.c0 is Fp4:
    # Cubic over quadratic
    # A = 3aÂ² âˆ’ 2 Ì„a
    # B = 3 âˆši cÂ² + 2 Ì„b
    # C = 3bÂ² âˆ’ 2 Ì„c
    var A{.noinit.}, B{.noinit.}, C{.noinit.}, D{.noinit.}: Fp4[C]

    A = a.c0

    r.c0.square(a.c0)  # r0 = aÂ²
    D.double(r.c0)     # D  = 2aÂ²
    r.c0 += D          # r0 = 3aÂ²

    A.conjneg()        # A = âˆ’ Ì„a
    A.double()         # A = âˆ’ 2 Ì„a
    r.c0 += A          # r0 = 3aÂ² âˆ’ 2 Ì„a

    B.square(a.c2)     # B = cÂ²
    B *= NonResidue    # B = âˆši cÂ²
    D.double(B)        # B = 2 âˆši cÂ²
    B += D             # B = 3 âˆši cÂ²

    r.c1.conj(a.c1)    # r1 = Ì„b
    r.c1.double()      # r1 = 2 Ì„b
    r.c1 += B          # r1 = 3 âˆši cÂ² + 2 Ì„b

    C.square(a.c1)     # C = bÂ²
    D.double(C)        # D = 2bÂ²
    C += D             # C = 3bÂ²

    r.c2.conjneg(a.c2) # r2 = - Ì„c
    r.c2.double()      # r2 = - 2 Ì„c
    r.c2 += C          # r2 = 3bÂ² - 2 Ì„c

  else:
    {.error: "Not implemented".}

func cyclotomic_square*[C](a: var Fp12[C]) =
  ## Square `a` into `r`
  ## `a` MUST be in the cyclotomic subgroup
  ## consequently `a` MUST be unitary
  #
  # Faster Squaring in the Cyclotomic Subgroup of Sixth Degree Extensions
  # Granger, Scott, 2009
  # https://eprint.iacr.org/2009/565.pdf

  when a.c0 is Fp4:
    # Cubic over quadratic
    # A = 3aÂ² âˆ’ 2 Ì„a
    # B = 3 âˆši cÂ² + 2 Ì„b
    # C = 3bÂ² âˆ’ 2 Ì„c
    var A{.noinit.}, B{.noinit.}, C{.noinit.}, D{.noinit.}: Fp4[C]

    A = a.c0

    a.c0.square()      # r0 = aÂ²
    D.double(a.c0)     # D  = 2aÂ²
    a.c0 += D          # r0 = 3aÂ²

    A.conjneg()        # A = âˆ’ Ì„a
    A.double()         # A = âˆ’ 2 Ì„a
    a.c0 += A          # r0 = 3aÂ² âˆ’ 2 Ì„a

    B.square(a.c2)     # B = cÂ²
    B *= NonResidue    # B = âˆši cÂ²
    D.double(B)        # B = 2 âˆši cÂ²
    B += D             # B = 3 âˆši cÂ²

    A = a.c1

    a.c1.conj()        # r1 = Ì„b
    a.c1.double()      # r1 = 2 Ì„b
    a.c1 += B          # r1 = 3 âˆši cÂ² + 2 Ì„b

    C.square(A)        # C = bÂ²
    D.double(C)        # D = 2bÂ²
    C += D             # C = 3bÂ²

    a.c2.conjneg()     # r2 = - Ì„c
    a.c2.double()      # r2 = - 2 Ì„c
    a.c2 += C          # r2 = 3bÂ² - 2 Ì„c

  else:
    {.error: "Not implemented".}

func cycl_sqr_repeated*(f: var Fp12, num: int) {.inline.} =
  ## Repeated cyclotomic squarings
  for _ in 0 ..< num:
    f.cyclotomic_square()

iterator unpack(scalarByte: byte): bool =
  yield bool((scalarByte and 0b10000000) shr 7)
  yield bool((scalarByte and 0b01000000) shr 6)
  yield bool((scalarByte and 0b00100000) shr 5)
  yield bool((scalarByte and 0b00010000) shr 4)
  yield bool((scalarByte and 0b00001000) shr 3)
  yield bool((scalarByte and 0b00000100) shr 2)
  yield bool((scalarByte and 0b00000010) shr 1)
  yield bool( scalarByte and 0b00000001)

func cyclotomic_exp*[C](r: var Fp12[C], a: Fp12[C], exponent: BigInt, invert: bool) =
    var eBytes: array[(exponent.bits+7) div 8, byte]
    eBytes.exportRawUint(exponent, bigEndian)

    r.setOne()
    for b in eBytes:
      for bit in unpack(b):
        r.cyclotomic_square()
        if bit:
          r *= a
    if invert:
      r.cyclotomic_inv()
