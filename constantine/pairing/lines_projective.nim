# Constantine
# Copyright (c) 2018-2019    Status Research & Development GmbH
# Copyright (c) 2020-Present Mamy AndrÃ©-Ratsimbazafy
# Licensed and distributed under either of
#   * MIT license (license terms in the root directory or at http://opensource.org/licenses/MIT).
#   * Apache v2 license (license terms in the root directory or at http://www.apache.org/licenses/LICENSE-2.0).
# at your option. This file may not be copied, modified, or distributed except according to those terms.

import
  ../primitives,
  ../config/curves,
  ../arithmetic,
  ../towers,
  ../elliptic/[
    ec_shortweierstrass_affine,
    ec_shortweierstrass_projective
  ],
  ./lines_common

export lines_common

# No exceptions allowed
{.push raises: [].}

# ############################################################
#
#            Miller Loop's Line Evaluation
#             with projective coordinates
#
# ############################################################
#
# - Pairing Implementation Revisited
#   Michael Scott, 2019
#   https://eprint.iacr.org/2019/077
#
# - The Realm of the Pairings
#   Diego F. Aranha and Paulo S. L. M. Barreto
#   and Patrick Longa and Jefferson E. Ricardini, 2013
#   https://eprint.iacr.org/2013/722.pdf
#   http://sac2013.irmacs.sfu.ca/slides/s1.pdf
#
# - Efficient Implementation of Bilinear Pairings on ARM Processors
#   Gurleen Grewal, Reza Azarderakhsh,
#   Patrick Longa, Shi Hu, and David Jao, 2012
#   https://eprint.iacr.org/2012/408.pdf

# Line evaluation only
# -----------------------------------------------------------------------------

func line_eval_double[F](
       line: var Line[F],
       T: ECP_ShortW_Prj[F, OnTwist]) =
  ## Evaluate the line function for doubling
  ## i.e. the tangent at T
  ##
  ## With T in homogenous projective coordinates (X, Y, Z)
  ## And Î¾ the sextic non residue to construct ğ”½p4 / ğ”½p6 / ğ”½p12
  ##
  ## M-Twist:
  ##   A = -2Î¾ Y.Z
  ##   B = 3bÎ¾ ZÂ² - YÂ²
  ##   C = 3 XÂ²
  ##
  ## D-Twist are scaled by Î¾ to avoid dividing by Î¾:
  ##   A = -2Î¾ Y.Z
  ##   B = 3b ZÂ² - Î¾YÂ²
  ##   C = 3Î¾ XÂ²
  ##
  ## Instead of
  ##   - equation 10 from The Real of pairing, Aranha et al, 2013
  ##   - or chapter 3 from pairing Implementation Revisited, Scott 2019
  ##   A = -2 Y.Z
  ##   B = 3b/Î¾ ZÂ² - YÂ²
  ##   C = 3 XÂ²
  ##
  ## A constant factor will be wiped by the final exponentiation
  ## as for all non-zero Î± âˆˆ GF(páµ)
  ## with
  ## - p odd prime
  ## - and gcd(Î±,páµ) = 1 (i.e. the extension field páµ is using irreducible polynomials)
  ##
  ## Little Fermat holds and we have
  ## Î±^(páµ - 1) â‰¡ 1 (mod páµ)
  ##
  ## The final exponent is of the form
  ## (páµ-1)/r
  ##
  ## A constant factor on twisted coordinates páµáŸáµˆ
  ## is a constant factor on páµ with d the twisting degree
  ## and so will be elminated. QED.
  var v {.noInit.}: F
  const b3 = 3 * F.C.getCoefB()

  template A: untyped = line.x
  template B: untyped = line.y
  template C: untyped = line.z

  A.prod(T.y, T.z)      # A = Y.Z
  C.square(T.x)         # C = XÂ²
  v.square(T.y)         # v = YÂ²
  B.square(T.z)         # B = ZÂ²

  A.double()            # A =  2 Y.Z
  A.neg()               # A = -2 Y.Z
  A *= SexticNonResidue # A = -2 Î¾ Y.Z

  B *= b3               # B = 3b ZÂ²
  C *= 3                # C = 3XÂ²
  when F.C.getSexticTwist() == M_Twist:
    B *= SexticNonResidue # B = 3b' ZÂ² = 3bÎ¾ ZÂ²
  elif F.C.getSexticTwist() == D_Twist:
    v *= SexticNonResidue # v =  Î¾ YÂ²
    C *= SexticNonResidue # C = 3Î¾ XÂ²
  else:
    {.error: "unreachable".}

  B -= v                # B = 3bÎ¾ ZÂ² - YÂ²  (M-twist)
                        # B = 3b ZÂ² - Î¾ YÂ² (D-twist)

func line_eval_add[F](
       line: var Line[F],
       T: ECP_ShortW_Prj[F, OnTwist],
       Q: ECP_ShortW_Aff[F, OnTwist]) =
  ## Evaluate the line function for addition
  ## i.e. the line between T and Q
  ##
  ## With T in homogenous projective coordinates (X, Y, Z)
  ## And Î¾ the sextic non residue to construct ğ”½p4 / ğ”½p6 / ğ”½p12
  ##
  ## M-Twist:
  ##   A = Î¾ (Xâ‚ - Zâ‚Xâ‚‚)
  ##   B = (Yâ‚ - Zâ‚Yâ‚‚) Xâ‚‚ - (Xâ‚ - Zâ‚Xâ‚‚) Yâ‚‚
  ##   C = - (Yâ‚ - Zâ‚Yâ‚‚)
  ##
  ## D-Twist:
  ##   A = Xâ‚ - Zâ‚Xâ‚‚
  ##   B = (Yâ‚ - Zâ‚Yâ‚‚) Xâ‚‚ - (Xâ‚ - Zâ‚Xâ‚‚) Yâ‚‚
  ##   C = - (Yâ‚ - Zâ‚Yâ‚‚)
  ##
  ## Note: There is no need for complete formula as
  ## we have T âˆ‰ [Q, -Q] in the Miller loop doubling-and-add
  ## i.e. the line cannot be vertical
  var v {.noInit.}: F

  template A: untyped = line.x
  template B: untyped = line.y
  template C: untyped = line.z

  v.prod(T.z, Q.y) # v = Zâ‚Yâ‚‚
  B.prod(T.z, Q.x) # B = Zâ‚Xâ‚‚

  A.diff(T.x, B)   # A = Xâ‚-Zâ‚Xâ‚‚
  C.diff(T.y, v)   # C = Yâ‚-Zâ‚Yâ‚‚

  v.prod(A, Q.y)   # v = (Xâ‚-Zâ‚Xâ‚‚) Yâ‚‚
  B.prod(C, Q.x)   # B = (Yâ‚-Zâ‚Yâ‚‚) Xâ‚‚
  B -= v           # B = (Yâ‚-Zâ‚Yâ‚‚) Xâ‚‚ - (Xâ‚-Zâ‚Xâ‚‚) Yâ‚‚

  C.neg()          # C = -(Yâ‚-Zâ‚Yâ‚‚)

  when F.C.getSexticTwist() == M_Twist:
    A *= SexticNonResidue # A = Î¾ (Xâ‚ - Zâ‚Xâ‚‚)

func line_eval_fused_double[Field](
       line: var Line[Field],
       T: var ECP_ShortW_Prj[Field, OnTwist]) =
  ## Fused line evaluation and elliptic point doubling
  # Grewal et al, 2012 adapted to Scott 2019 line notation
  var A {.noInit.}, B {.noInit.}, C {.noInit.}: Field
  var E {.noInit.}, F {.noInit.}, G {.noInit.}: Field
  template H: untyped = line.x
  const b3 = 3*Field.C.getCoefB()

  var snrY = T.y
  when Field.C.getSexticTwist() == D_Twist:
    snrY *= SexticNonResidue

  A.prod(T.x, snrY)
  A.div2()          # A = XY/2
  B.square(T.y)     # B = YÂ²
  C.square(T.z)     # C = ZÂ²

  var snrB = B
  when Field.C.getSexticTwist() == D_Twist:
    snrB *= SexticNonResidue

  E.prod(C, b3)
  when Field.C.getSexticTwist() == M_Twist:
    E *= SexticNonResidue # E = 3b'ZÂ² = 3bÎ¾ ZÂ²

  F.prod(E, 3)      # F = 3E = 9bZÂ²
  G.sum(snrB, F)
  G.div2()          # G = (B+F)/2
  H.sum(T.y, T.z)
  H.square()
  H -= B
  H -= C            # lx = H = (Y+Z)Â²-(B+C)= 2YZ

  line.z.square(T.x)
  line.z *= 3       # lz = 3XÂ²
  when Field.C.getSexticTwist() == D_Twist:
    line.z *= SexticNonResidue

  line.y.diff(E, snrB) # ly = E-B = 3b'ZÂ² - YÂ²

  # In-place modification: invalidates `T.` calls
  T.x.diff(snrB, F)
  T.x *= A          # Xâ‚ƒ = A(B-F) = XY/2.(YÂ²-9b'ZÂ²)
                    # M-twist: XY/2.(YÂ²-9bÎ¾ZÂ²)
                    # D-Twist: Î¾XY/2.(YÂ²Î¾-9bZÂ²)

  T.y.square(G)
  E.square()
  E *= 3
  T.y -= E          # Yâ‚ƒ = GÂ² - 3EÂ² = (YÂ²+9b'ZÂ²)Â²/4 - 3*(3b'ZÂ²)Â²
                    # M-twist: (YÂ²+9bÎ¾ZÂ²)Â²/4 - 3*(3bÎ¾ZÂ²)Â²
                    # D-Twist: (Î¾YÂ²+9bZÂ²)Â²/4 - 3*(3bZÂ²)Â²

  when Field.C.getSexticTwist() == D_Twist:
    H *= SexticNonResidue
  T.z.prod(snrB, H) # Zâ‚ƒ = BH = YÂ²((Y+Z)Â² - (YÂ²+ZÂ²)) = 2YÂ³Z
                    # M-twist: 2YÂ³Z
                    # D-twist: 2Î¾Â²YÂ³Z

  # Correction for Fp4 towering
  H.neg()           # lx = -H
  when Field.C.getSexticTwist() == M_Twist:
    H *= SexticNonResidue
    # else: the SNR is already integrated in H

func line_eval_fused_add[Field](
       line: var Line[Field],
       T: var ECP_ShortW_Prj[Field, OnTwist],
       Q: ECP_ShortW_Aff[Field, OnTwist]) =
  ## Fused line evaluation and elliptic point addition
  # Grewal et al, 2012 adapted to Scott 2019 line notation
  var
    A {.noInit.}: Field
    B {.noInit.}: Field
    C {.noInit.}: Field
    D {.noInit.}: Field
    E {.noInit.}: Field
    F {.noInit.}: Field
    G {.noInit.}: Field
    H {.noInit.}: Field
    I {.noInit.}: Field

  template lambda: untyped = line.x
  template theta: untyped = line.z
  template J: untyped = line.y

  A.prod(Q.y, T.z)
  B.prod(Q.x, T.z)
  theta.diff(T.y, A)  # Î¸ = Yâ‚ - Zâ‚Xâ‚‚
  lambda.diff(T.x, B) # Î» = Xâ‚ - Zâ‚Xâ‚‚
  C.square(theta)
  D.square(lambda)
  E.prod(D, lambda)
  F.prod(T.z, C)
  G.prod(T.x, D)
  H.double(G)
  H.diff(F, H)
  H += E
  I.prod(T.y, E)

  T.x.prod(theta, Q.x)
  T.y.prod(lambda, Q.y)
  J.diff(T.x, T.y)

  # EC addition
  T.x.prod(lambda, H)

  T.y.diff(G, H)
  T.y *= theta
  T.y -= I

  T.z *= E

  # Line evaluation
  theta.neg()
  when Field.C.getSexticTwist() == M_Twist:
    lambda *= SexticNonResidue # A = Î¾ (Xâ‚ - Zâ‚Xâ‚‚)

# Public proc
# -----------------------------------------------------------------------------

func line_double*[F1, F2](
       line: var Line[F2],
       T: var ECP_ShortW_Prj[F2, OnTwist],
       P: ECP_ShortW_Aff[F1, NotOnTwist]) =
  ## Doubling step of the Miller loop
  ## T in G2, P in G1
  ##
  ## Compute lt,t(P)
  static: doAssert F1.C == F2.C
  when true:
    line_eval_fused_double(line, T)
    line.line_update(P)
  else:
    line_eval_double(line, T)
    line.line_update(P)
    T.double()

func line_add*[F1, F2](
       line: var Line[F2],
       T: var ECP_ShortW_Prj[F2, OnTwist],
       Q: ECP_ShortW_Aff[F2, OnTwist],
       P: ECP_ShortW_Aff[F1, NotOnTwist]) =
  ## Addition step of the Miller loop
  ## T and Q in G2, P in G1
  ##
  ## Compute lt,q(P)
  static: doAssert F1.C == F2.C
  when true:
    line_eval_fused_add(line, T, Q)
    line.line_update(P)
  else:
    line_eval_add(line, T, Q)
    line.line_update(P)
    T += Q
