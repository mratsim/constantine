# Constantine
# Copyright (c) 2018-2019    Status Research & Development GmbH
# Copyright (c) 2020-Present Mamy Andr√©-Ratsimbazafy
# Licensed and distributed under either of
#   * MIT license (license terms in the root directory or at http://opensource.org/licenses/MIT).
#   * Apache v2 license (license terms in the root directory or at http://www.apache.org/licenses/LICENSE-2.0).
# at your option. This file may not be copied, modified, or distributed except according to those terms.

import
  constantine/named/algebras,
  ../math/[ec_shortweierstrass, arithmetic, extension_fields],
  ../math/elliptic/[ec_multi_scalar_mul, ec_shortweierstrass_batch_ops],
  ../math/pairings/pairings_generic,
  ../named/zoo_generators,
  ../math/polynomials/polynomials,
  ../platforms/abstractions,
  ./protocol_quotient_check

## ############################################################
##
##                 KZG Polynomial Commitments
##
## ############################################################
##
## This module implements KZG-inspired Polynomial commitments (Kate, Zaverucha, Goldberg)
##
## - KZG Paper:
##   Constant-Size Commitments to Polynomials and Their Applications
##   Kate, Zaverucha, Goldberg, 2010
##   https://www.iacr.org/archive/asiacrypt2010/6477178/6477178.pdf
##   https://cacr.uwaterloo.ca/techreports/2010/cacr2010-10.pdf
##
##
## KZG-inspired protocol
## ------------------------------------------------------------
##
## Quick algebra refresher for developers
## ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
##
## - A group is a set of elements:
##   - with a binary operation to combine them called the group law
##   - with a neutral element
##   - with an inverse, applying the group law on an element and its inverse results in the neutral element.
##
##   - the group order or cardinality is the number of elements in the set
##   - the group can use the additive or multiplicative notation.
##   - the group can be cyclic. i.e. all elements of the group can be generated
##     by repeatedly applying the group law.
##
##   The additive/multiplicative notation is chosen by social consensus,
##   hence confusion of scalar multiplication [a]P or exponentiation P·µÉ for elliptic curves.
##
## - A field is a set of elements
##   - with two group laws, addition and multiplication
##   - and the corresponding group properties (additive/multiplicative inverse and neutral elements)
##
##   - A field can be finite (modular arithmetic modulo a prime) or infinite (the real numbers)
##
## Sigil refreshers for developers
## ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
##
## - ‚àÉ: there exists
## - ‚àÄ: for all
## - ‚àà: element of
##
## Notation
## ~~~~~~~~
##
##   - ùîΩr is a finite-field of prime order r
##   - ùîæ1 is an additive group of prime order r
##   - ùîæ2 is an additive group of prime order r
##   - ùîæt is a multiplicative group of prime order r
##
##   In practice:
##     - ‚àÄ(x, y) such that y¬≤ = x¬≥ + b has a cyclic group of r solutions, the group ùîæ1 (of the elliptic curve E1)
##     - ‚àÄ(x', y') such that y'¬≤ = x'¬≥ + b' has a cyclic group of r solutions, the group ùîæ2 (of the elliptic curve E2)
##     - ùîæt is also a cyclic subgroup of order r
##     - r is the (large prime) number of elements in all those subgroups.
##
## - Implementation details (for the very curious)
##     - For ùîæ1, (x, y) ‚àà (ùîΩp, ùîΩp)
##     - For ùîæ2, (x', y') ‚àà (ùîΩp‚Åø, ùîΩp‚Åø) with n = 2 usually (BN and BLS12 curves), but it can be 1 (BW6 curves), 4 (BLS24 curves) or ...
##     - ùîæt is the cyclotomic subgroup over ùîΩp·µè, k being the curve embedding degree, with k = 12 usually (BN and BLS12 curves) but it can be 6 (BW6 curves), 24 (BLS24 curves) or ...
##     - p is completely unused in the protocol so don't use mental space to keep these details.
##
##   We use the notation:
##     [a]P to represent P+P+ .... + P
##     Applying the group law `a` times, i.e. the scalar multiplication.
##
##   There exist a pairing function (bilinear map)
##     e: ùîæ1 x ùîæ2 -> ùîæt
##   That map is bilinear
##   ‚àÄa ‚àà ùîΩr, ‚àÄb ‚àà ùîΩr, ‚àÄP ‚àà ùîæ1, ‚àÄQ ‚àà ùîæ2,
##   e([a]P, [b]Q) = e(P, Q)·µÉ·µá
##
##   We use the notation:
##     G‚ÇÅ for the protocol-chosen generator of ùîæ1
##     G‚ÇÇ for the protocol-chosen generator of ùîæ2
##     [a]‚ÇÅ for the scalar multiplication of the ùîæ1 generator by a, a ‚àà ùîΩr
##     [b]‚ÇÇ for the scalar multiplication of the ùîæ2 generator by b, b ‚àà ùîΩr
##
## Polynomial Commitment Scheme
## ~~~~~~~~~~~~~~~~~~~~~~~~~~~~
##
## We have 2 parties, a Prover and a Verifier.
##
## They share a public Structured Reference String (SRS), also called trusted setup:
##   srs_g1: [[1]‚ÇÅ, [œÑ]‚ÇÅ, [œÑ¬≤]‚ÇÅ, ... [œÑ‚Åø‚Åª¬π]‚ÇÅ] also called powers of tau, with a bounded degree n-1
##   srs_g2: [[1]‚ÇÇ, [œÑ]‚ÇÇ]
##
## œÑ and its powers are secrets that no one knows, we only work with [œÑ‚Å±]‚ÇÅ and [œÑ]‚ÇÇ
## not with œÑ directly. (œÑ cannot be deduced due to the elliptic curve discrete logarithm problem)
##
## Info
##   œÑ and its powers are created through a secure multi-party computation (MPC) ceremony
##   called powers of tau. Each participant contributes randomness.
##   Only one honest participant (who ensures that the randomness created cannot be recreated)
##   is necessary for the success of the ceremony.
##
## Protocol
##
## 0. A data blob is interpreted as up to n ùîΩr elements
##    corresponding to a polynomial p(x) = blob‚ÇÄ + blob‚ÇÅ x + blob‚ÇÇ x¬≤ + ... + blob‚Çô‚Çã‚ÇÅ x‚Åø‚Åª¬π
##                                  p(x) = ‚àë‚ÇÄ‚Åø‚Åª¬π blob·µ¢ x‚Å±
##
##    So we can commit/prove up to n*log‚ÇÇ(r) bits of data
##    For Ethereum, n = 4096 and log‚ÇÇ(r) = 255 bits
##    so 130.560kB of transaction data committed per 48B proof stored in the blockchain
##
## 1. commit(srs_g1, blob) -> commitment C = ‚àë blob·µ¢.srs_g1·µ¢ = ‚àë [blob·µ¢.œÑ‚Å±]‚ÇÅ = [p(œÑ)]‚ÇÅ
##
## 2. The verifier chooses a random opening_challenge `z` in ùîΩr that the prover does not control.
##    To make the protocol non-interactive, z may be computed via the Fiat-Shamir heuristic.
##
## 3. compute_proof(blob, [commitment]‚ÇÅ, opening_challenge) -> (eval_at_challenge, [proof]‚ÇÅ)
##      blob: p(x)
##      [commitment]‚ÇÅ: [p(œÑ)]‚ÇÅ
##      opening_challenge: z
##      ~~~~~~~~~~~~~~~~~~~~~~~~~~~~
##      -> The prover needs to provide a proof that it knows a polynomial p(x)
##         such that p(z) = y. With the proof, the verifier doesn't need access to the polynomial to verify the claim.
##      -> Compute a witness polynomial w(x, z) = (p(x) - p(z)) / (x-z)
##         We can evaluate it at œÑ from the public SRS and opening_challenge point `z` chosen by the verifier (indifferentiable from random).
##         We don't know œÑ, but we know [œÑ]‚ÇÅ so we transport the problem from ùîΩr to ùîæ1
##      => The proof is the evaluation of the witness polynomial for a opening_challenge `z` of the verifier choosing.
##         w(œÑ, z) = proof
##         We output [proof]‚ÇÅ = [proof]G‚ÇÅ
##
## 4. verify_commitment([commitment]‚ÇÅ, opening_challenge, eval_at_challenge, [proof]‚ÇÅ) -> bool
##      [commitment]‚ÇÅ: [p(œÑ)]‚ÇÅ
##      opening_challenge: z
##      eval_at_challenge: p(z) = y
##      [proof]‚ÇÅ: [(p(œÑ) - p(z)) / (œÑ-z)]‚ÇÅ
##      ~~~~~~~~~~~~~~~~~~~~~~~~~~~~
##      -> proof = w(œÑ, z) = (p(œÑ) - p(z)) / (œÑ-z) = (p(œÑ) - y) / (œÑ-z)
##         hence proof.(œÑ-z) = p(œÑ) - y
##
##      => using a bilinear pairing function e(ùîæ1, ùîæ2)->ùîæt we can rewrite this equality to
##         e([proof]‚ÇÅ, [œÑ]‚ÇÇ - [z]‚ÇÇ) = e(C - [y]‚ÇÅ, [1]‚ÇÇ)
##
##      According to the Schwartz-zippel Lemma it is cryptographically unlikely
##      that this equation holds unless what the prover provided for [commitment]‚ÇÅ = [p(œÑ)]‚ÇÅ
##
## Variants
## - srs_g1 and blob may be either polynomial in monomial basis
##   p(x) = blob‚ÇÄ + blob‚ÇÅ x + blob‚ÇÇ x¬≤ + ... + blob‚Çô‚Çã‚ÇÅ x‚Åø‚Åª¬π
## - or polynomial in Lagrange basis, defined over tuples
##   [(œâ‚Å∞, p(œâ‚Å∞)), (œâ¬π, p(œâ¬π)), (œâ¬≤, p(œâ¬≤)), ..., (œâ‚Åø‚Åª¬π, p(œâ‚Åø‚Åª¬π))]
##   with œâ ‚àà ùîΩr a root of unity of order n, i.e. œâ‚Åø = 1

# KZG - Prover - Lagrange basis
# ------------------------------------------------------------
#
# For now we assume that the input polynomial always has the same degree
# as the powers of œÑ

func kzg_commit*[N, bits: static int, Name: static Algebra](
       powers_of_tau: PolynomialEval[N, EC_ShortW_Aff[Fp[Name], G1]],
       commitment: var EC_ShortW_Aff[Fp[Name], G1],
       poly: PolynomialEval[N, BigInt[bits]]) {.tags:[Alloca, HeapAlloc, Vartime].} =
  var commitmentJac {.noInit.}: EC_ShortW_Jac[Fp[Name], G1]
  commitmentJac.multiScalarMul_vartime(poly.evals, powers_of_tau.evals)
  commitment.affine(commitmentJac)

func kzg_prove*[N: static int, Name: static Algebra](
       powers_of_tau: PolynomialEval[N, EC_ShortW_Aff[Fp[Name], G1]],
       domain: PolyEvalRootsDomain[N, Fr[Name]],
       eval_at_challenge: var Fr[Name],
       proof: var EC_ShortW_Aff[Fp[Name], G1],
       poly: PolynomialEval[N, Fr[Name]],
       opening_challenge: Fr[Name]) {.tags:[Alloca, HeapAlloc, Vartime].} =

  let quotientPoly = allocHeapAligned(PolynomialEval[N, Fr[Name]], alignment = 64)

  domain.getQuotientPoly(
    quotientPoly[], eval_at_challenge,
    poly, opening_challenge
  )

  var proofJac {.noInit.}: EC_ShortW_Jac[Fp[Name], G1]
  proofJac.multiScalarMul_vartime(quotientPoly.evals, powers_of_tau.evals)
  proof.affine(proofJac)

  freeHeapAligned(quotientPoly)

# KZG - Verifier
# ------------------------------------------------------------

func kzg_verify*[F2; Name: static Algebra](
       commitment: EC_ShortW_Aff[Fp[Name], G1],
       opening_challenge: BigInt, # Fr[Name].getBigInt(),
       eval_at_challenge: BigInt, # Fr[Name].getBigInt(),
       proof: EC_ShortW_Aff[Fp[Name], G1],
       tauG2: EC_ShortW_Aff[F2, G2]): bool {.tags:[Alloca, Vartime].} =
  ## Verify a short KZG proof that ``p(opening_challenge) = eval_at_challenge``
  ## without doing the whole p(opening_challenge) computation
  #
  # Scalar inputs
  #   opening_challenge
  #   eval_at_challenge = p(opening_challenge)
  #
  # Group inputs
  #   [commitment]‚ÇÅ = [p(œÑ)]G
  #   [proof]‚ÇÅ = [proof]G
  #   [œÑ]‚ÇÇ = [œÑ]H in the trusted setup
  #
  # With z = opening_challenge, we want to verify
  #   proof.(œÑ - z) = p(œÑ)-p(z)
  #
  # However œÑ is a secret from the trusted setup that cannot be used raw.
  # We transport the equation in the pairing group ùîæT with bilinear pairings e
  #
  # e([proof]‚ÇÅ, [œÑ]‚ÇÇ - [z]‚ÇÇ) = e([p(œÑ)]‚ÇÅ - [p(z)]‚ÇÅ, [1]‚ÇÇ)
  # e([proof]‚ÇÅ, [œÑ]‚ÇÇ - [z]‚ÇÇ) . e([p(œÑ)]‚ÇÅ - [p(z)]‚ÇÅ, [-1]‚ÇÇ) = 1
  #
  # Finally
  #   e([proof]‚ÇÅ, [œÑ]‚ÇÇ - [opening_challenge]‚ÇÇ) . e([commitment]‚ÇÅ - [eval_at_challenge]‚ÇÅ, [-1]‚ÇÇ) = 1
  var
    tau_minus_challenge_G2 {.noInit.}: EC_ShortW_Jac[F2, G2]
    commitment_minus_eval_at_challenge_G1 {.noInit.}: EC_ShortW_Jac[Fp[Name], G1]
    negG2 {.noInit.}: EC_ShortW_Aff[F2, G2]

    tauG2Jac {.noInit.}: EC_ShortW_Jac[F2, G2]
    commitmentJac {.noInit.}: EC_ShortW_Jac[Fp[Name], G1]

  tau_minus_challenge_G2.setGenerator()
  commitment_minus_eval_at_challenge_G1.setGenerator()
  negG2.neg(Name.getGenerator("G2"))
  tauG2Jac.fromAffine(tauG2)
  commitmentJac.fromAffine(commitment)

  tau_minus_challenge_G2.scalarMul_vartime(opening_challenge)
  tau_minus_challenge_G2.diff(tauG2Jac, tau_minus_challenge_G2)

  commitment_minus_eval_at_challenge_G1.scalarMul_vartime(eval_at_challenge)
  commitment_minus_eval_at_challenge_G1.diff(commitmentJac, commitment_minus_eval_at_challenge_G1)

  var tmzG2 {.noInit.}: EC_ShortW_Aff[F2, G2]
  var cmyG1 {.noInit.}: EC_ShortW_Aff[Fp[Name], G1]
  tmzG2.affine(tau_minus_challenge_G2)
  cmyG1.affine(commitment_minus_eval_at_challenge_G1)

  # e([proof]‚ÇÅ, [œÑ]‚ÇÇ - [opening_challenge]‚ÇÇ) * e([commitment]‚ÇÅ - [eval_at_challenge]‚ÇÅ, [-1]‚ÇÇ)
  var gt {.noInit.}: Name.getGT()
  gt.pairing([proof, cmyG1], [tmzG2, negG2])

  return gt.isOne().bool()

func kzg_verify_batch*[bits: static int, F2; Name: static Algebra](
       commitments: ptr UncheckedArray[EC_ShortW_Aff[Fp[Name], G1]],
       challenges: ptr UncheckedArray[Fr[Name]],
       evals_at_challenges: ptr UncheckedArray[BigInt[bits]],
       proofs: ptr UncheckedArray[EC_ShortW_Aff[Fp[Name], G1]],
       linearIndepRandNumbers: ptr UncheckedArray[Fr[Name]],
       n: int,
       tauG2: EC_ShortW_Aff[F2, G2]): bool {.tags:[HeapAlloc, Alloca, Vartime].} =
  ## Verify multiple KZG proofs efficiently
  ##
  ## Parameters
  ##
  ## `n` verification sets
  ## A verification set i (commitment·µ¢, challenge·µ¢, eval_at_challenge·µ¢, proof·µ¢)
  ## is passed in a "struct-of-arrays" fashion.
  ##
  ## Notation:
  ##   i ‚àà [0, n), a verification set with ID i
  ##   [a]‚ÇÅ corresponds to the scalar multiplication [a]G by the generator G of the group ùîæ1
  ##
  ## - `commitments`: `n` commitments [commitment·µ¢]‚ÇÅ
  ## - `challenges`: `n` challenges z·µ¢
  ## - `evals_at_challenges`: `n` evaluation y·µ¢ = p·µ¢(z·µ¢)
  ## - `proofs`: `n` [proof]‚ÇÅ
  ## - `linearIndepRandNumbers`: `n` linearly independant numbers that are not in control
  ##                               of a prover (potentially malicious).
  ## - `n`: the number of verification sets
  ##
  ## For all (commitment·µ¢, challenge·µ¢, eval_at_challenge·µ¢, proof·µ¢),
  ## we verify the relation
  ##   proof·µ¢.(œÑ - z·µ¢) = p·µ¢(œÑ)-p·µ¢(z·µ¢)
  ##
  ## As œÑ is the secret from the trusted setup, boxed in [œÑ]‚ÇÅ and [œÑ]‚ÇÇ,
  ## we rewrite the equality check using pairings
  ##
  ##   e([proof·µ¢]‚ÇÅ, [œÑ]‚ÇÇ - [challenge·µ¢]‚ÇÇ) . e([commitment·µ¢]‚ÇÅ - [eval_at_challenge·µ¢]‚ÇÅ, [-1]‚ÇÇ) = 1
  ##
  ## Or batched using Feist-Khovratovich method
  ##
  ##  e(‚àë [r·µ¢][proof·µ¢]‚ÇÅ, [œÑ]‚ÇÇ) . e(‚àë[r·µ¢]([commitment·µ¢]‚ÇÅ - [eval_at_challenge·µ¢]‚ÇÅ) + ‚àë[r·µ¢][z·µ¢][proof·µ¢]‚ÇÅ, [-1]‚ÇÇ) = 1
  #
  # Described in:
  # - https://github.com/ethereum/consensus-specs/blob/v1.4.0-beta.1/specs/deneb/polynomial-commitments.md#verify_kzg_proof_batch
  # - https://dankradfeist.de/ethereum/2021/06/18/pcs-multiproofs.html]\
  # - Fast amortized KZG proofs
  #   Feist, Khovratovich
  #   https://eprint.iacr.org/2023/033
  # - https://alinush.github.io/2021/06/17/Feist-Khovratovich-technique-for-computing-KZG-proofs-fast.html

  static: doAssert BigInt[bits] is Fr[Name].getBigInt()

  var sums_jac {.noInit.}: array[2, EC_ShortW_Jac[Fp[Name], G1]]
  template sum_rand_proofs: untyped = sums_jac[0]
  template sum_commit_minus_evals_G1: untyped = sums_jac[1]
  var sum_rand_challenge_proofs {.noInit.}: EC_ShortW_Jac[Fp[Name], G1]

  # ‚àë [r·µ¢][proof·µ¢]‚ÇÅ
  # ---------------
  let coefs = allocHeapArrayAligned(Fr[Name].getBigInt(), n, alignment = 64)
  coefs.batchFromField(linearIndepRandNumbers, n)
  sum_rand_proofs.multiScalarMul_vartime(coefs, proofs, n)

  # ‚àë[r·µ¢]([commitment·µ¢]‚ÇÅ - [eval_at_challenge·µ¢]‚ÇÅ)
  # ---------------------------------------------
  #
  # We interleave allocation and deallocation, which hurts cache reuse
  # i.e. when alloc is being done, it's better to do all allocs as the metadata will already be in cache
  #
  # but it's more important to minimize memory usage especially if we want to commit with 2^26+ points
  #
  # We dealloc in reverse alloc order, to avoid leaving holes in the allocator pages.
  let commits_min_evals = allocHeapArrayAligned(EC_ShortW_Aff[Fp[Name], G1], n, alignment = 64)
  let commits_min_evals_jac = allocHeapArrayAligned(EC_ShortW_Jac[Fp[Name], G1], n, alignment = 64)

  for i in 0 ..< n:
    commits_min_evals_jac[i].fromAffine(commitments[i])
    var boxed_eval {.noInit.}: EC_ShortW_Jac[Fp[Name], G1]
    boxed_eval.setGenerator()
    boxed_eval.scalarMul_vartime(evals_at_challenges[i])
    commits_min_evals_jac[i].diff_vartime(commits_min_evals_jac[i], boxed_eval)

  commits_min_evals.batchAffine(commits_min_evals_jac, n)
  freeHeapAligned(commits_min_evals_jac)
  sum_commit_minus_evals_G1.multiScalarMul_vartime(coefs, commits_min_evals, n)
  freeHeapAligned(commits_min_evals)

  # ‚àë[r·µ¢][z·µ¢][proof·µ¢]‚ÇÅ
  # ------------------
  var tmp {.noInit.}: Fr[Name]
  for i in 0 ..< n:
    tmp.prod(linearIndepRandNumbers[i], challenges[i])
    coefs[i].fromField(tmp)

  sum_rand_challenge_proofs.multiScalarMul_vartime(coefs, proofs, n)
  freeHeapAligned(coefs)

  # e(‚àë [r·µ¢][proof·µ¢]‚ÇÅ, [œÑ]‚ÇÇ) . e(‚àë[r·µ¢]([commitment·µ¢]‚ÇÅ - [eval_at_challenge·µ¢]‚ÇÅ) + ‚àë[r·µ¢][z·µ¢][proof·µ¢]‚ÇÅ, [-1]‚ÇÇ) = 1
  # -----------------------------------------------------------------------------------------------------------
  template sum_of_sums: untyped = sums_jac[1]

  sum_of_sums.sum_vartime(sum_commit_minus_evals_G1, sum_rand_challenge_proofs)

  var sums {.noInit.}: array[2, EC_ShortW_Aff[Fp[Name], G1]]
  sums.batchAffine(sums_jac)

  var negG2 {.noInit.}: EC_ShortW_Aff[F2, G2]
  negG2.neg(Name.getGenerator("G2"))

  var gt {.noInit.}: Name.getGT()
  gt.pairing(sums, [tauG2, negG2])

  return gt.isOne().bool()
